#+TITLE: Emacs config (single source of truth)
#+PROPERTY: header-args:emacs-lisp :lexical t
#+OPTIONS: toc:nil
* early-init.el
:PROPERTIES:
:header-args:emacs-lisp: :tangle early-init.el
:END:
#+begin_src emacs-lisp
;; -*- lexical-binding: t; -*-
;;; early-init.el - 激进优化版
(defvar my/startup-gc-cons-threshold gc-cons-threshold)
(defvar my/startup-gc-cons-percentage gc-cons-percentage)
(setq gc-cons-threshold most-positive-fixnum
      gc-cons-percentage 0.6)
(add-hook 'emacs-startup-hook
          (lambda ()
            (setq gc-cons-threshold (* 64 1024 1024)  ;; 64MB（更激进）
                  gc-cons-percentage 0.1)))
(defvar my/startup-file-name-handler-alist file-name-handler-alist)
(setq file-name-handler-alist nil)

(add-hook 'emacs-startup-hook
          (lambda ()
            (setq file-name-handler-alist my/startup-file-name-handler-alist)))
(setq package-quickstart t)
;; 首次使用需运行: M-x package-quickstart-refresh
(setq load-prefer-newer t
      ;;package-enable-at-startup nil
      native-comp-jit-compilation t
      native-comp-async-report-warnings-errors nil
      native-comp-deferred-compilation t)  ;; 延迟编译
(prefer-coding-system 'utf-8)
(setq-default buffer-file-coding-system 'utf-8)
(setq frame-inhibit-implied-resize t)
(push '(menu-bar-lines . 0) default-frame-alist)
(push '(tool-bar-lines . 0) default-frame-alist)
(push '(vertical-scroll-bars . nil) default-frame-alist)
(setq load-suffixes '(".elc" ".el"))  ;; 优先 .elc
(setq load-file-rep-suffixes '(""))
(provide 'early-init)
;;; early-init.el ends here
#+end_src

* init.el
:PROPERTIES:
:header-args:emacs-lisp: :tangle init.el
:END:
#+begin_src emacs-lisp
;; -*- lexical-binding: t; -*-
(require 'package)
(setq package-archives '(("gnu"    . "https://mirrors.ustc.edu.cn/elpa/gnu/")
                         ("melpa"  . "https://mirrors.ustc.edu.cn/elpa/melpa/")
                         ("nongnu" . "https://mirrors.ustc.edu.cn/elpa/nongnu/")))
(setq package-native-compile t              ;; 使用原生编译
      package-install-upgrade-built-in t) ;; 升级内置包
(eval-and-compile
  (unless (package-installed-p 'use-package)
    (package-refresh-contents)  ;; 仅首次运行时刷新
    (package-install 'use-package))

  (require 'use-package)
  (setq use-package-always-ensure nil       ;; 关键：禁用自动安装
        use-package-always-defer t          ;; 关键：默认延迟加载
        use-package-expand-minimally t      ;; 最小化展开
        use-package-compute-statistics nil)) ;; 关闭统计
#+END_SRC
** 内置行为与基础设置
*** 核心设置
#+begin_src emacs-lisp
;; 命令行选项
(setq command-line-x-option-alist nil)
;; 进程 IO 优化
(setq read-process-output-max #x10000)  ; 64kb
;; 禁用 ffap 域名检测
(setq ffap-machine-p-known 'reject)
;; 个人信息
(setq user-full-name "Wei"
      user-mail-address "Jiawei0655@gmail.com")
;; UTF-8 编码
(when (fboundp 'set-charset-priority)
  (set-charset-priority 'unicode))
(prefer-coding-system 'utf-8)
(setq locale-coding-system 'utf-8)
(setq system-time-locale "C")
(use-package gcmh
  :ensure t
  :demand t
  :diminish
  :hook (emacs-startup . gcmh-mode)
  :custom
  (gcmh-idle-delay 'auto)
  (gcmh-auto-idle-delay-factor 10)
  (gcmh-high-cons-threshold #x12800000))
;; Server 模式
(use-package server
  :ensure nil
  :hook (after-init . server-mode))
;; 保存位置
(use-package saveplace
  :defer t
  :hook (after-init . save-place-mode)
  :config (setq save-place-file (locate-user-emacs-file "etc/places")))
;; 最近文件
(use-package recentf
  :defer 1
  :bind (("C-x C-r" . recentf-open-files))
  :hook (after-init . recentf-mode)
  :custom
  (recentf-max-saved-items 300)
  (recentf-auto-cleanup 'never)
  (recentf-exclude
   '("^/private/tmp/"
     "^/var/folders/"
     "^/tmp/"
     "/ssh\\(x\\)?:"
     "/su\\(do\\)?:"
     "^/usr/include/"
     "/TAGS\\'"
     "COMMIT_EDITMSG\\'"))
  :config (setq recentf-save-file (locate-user-emacs-file "etc/recentf")))
;; 历史记录
(use-package savehist
  :hook (after-init . savehist-mode)
  :init (setq enable-recursive-minibuffers t
              history-length 1000
              savehist-file (locate-user-emacs-file "etc/history")
              savehist-additional-variables '(mark-ring
                                              global-mark-ring
                                              search-ring
                                              regexp-search-ring
                                              extended-command-history)
              savehist-autosave-interval 300))
#+end_src

*** 使用地址
#+BEGIN_SRC emacs-lisp
(require 'cl-lib)
(defun my/normalize-path (PATH)
  "Convert a Windows PATH to Emacs-style forward slashes and ensure a trailing slash."
  (file-name-as-directory
   (replace-regexp-in-string "\\\\" "/" (convert-standard-filename PATH))))
;; --- Root directories ---
(defconst my/org-root
  (my/normalize-path "C:/Users/wei/Documents/org/")
  "Org root directory.")
(defconst my/org-agenda-root
  (my/normalize-path (expand-file-name "agenda" my/org-root))
  "Org-agenda root directory.")
(defconst my/denote-root
  (my/normalize-path (expand-file-name "notes" my/org-root))
  "Denote notes root directory.")
;; --- Agenda sub-directories ---
(defconst my/org-projects-dir (expand-file-name "projects" my/org-agenda-root)
  "Projects directory under agenda.")
(defconst my/org-work-dir     (expand-file-name "work"     my/org-agenda-root)
  "Work directory under agenda.")
(defconst my/org-personal-dir (expand-file-name "personal" my/org-agenda-root)
  "Personal directory under agenda.")
;; --- Bibliography directories ---
(defconst my/bibliography-directory
  (expand-file-name "bibliography" my/org-root)
  "Bibliography library directory.")
(defconst my/bibliography-files
  (list (expand-file-name "我的文库.bib" my/bibliography-directory))
  "List of main BibTeX files.")
(defconst my/bibliography-notes-directory
  (expand-file-name "literature-notes" my/denote-root)
  "Directory for literature notes (used with Denote).")
;; --- GPTel cache directory ---
(defconst my/gptel-cache-dir
  (expand-file-name "etc/gptel" user-emacs-directory)
  "GPTel cache directory.")
;; --- Unified directory list (duplicates removed) ---
(defconst my/managed-directories
  (delete-dups
   (list my/org-root
         my/org-agenda-root
         my/denote-root
         my/org-projects-dir
         my/org-work-dir
         my/org-personal-dir
         my/bibliography-directory
         my/bibliography-notes-directory
         my/gptel-cache-dir))
  "Directories that must exist on startup.")
(defun my/ensure-directories ()
  "Ensure all directories in `my/managed-directories' exist.
Missing ones are created once; this function removes itself from
`after-init-hook' afterwards to avoid repeated checks."
  (let ((missing (cl-remove-if #'file-directory-p my/managed-directories)))
    (when missing
      (dolist (dir missing)
        (make-directory dir t))))
  (remove-hook 'after-init-hook #'my/ensure-directories))
(add-hook 'after-init-hook #'my/ensure-directories)
#+end_src
** UI 与显示
*** 主题配置
#+begin_src emacs-lisp
(use-package panel
  :vc (panel :url "https://github.com/jiandwei/emacs-panel.git"
             :branch "main")
  :hook (after-init . panel-create-hook)
  :config
  ;; ========== 基础显示配置 ==========
  (setq panel-title "Happy hacking, Emacs ♥  you"
        panel-show-file-path nil
        panel-min-left-padding 10
        panel-path-max-length 20
        ;; 地理位置 - 大连
        panel-latitude 38.9140
        panel-longitude 121.6147
        ;; 图片配置
        panel-image-file (locate-user-emacs-file "etc/bitmap.png")
        panel-image-width 400
        panel-image-height 169))

(use-package circadian
  :ensure t
  :defer t
  :init
  (setq circadian-themes
        '((:sunrise . modus-operandi-tinted)
          (:sunset  . modus-vivendi))
        calendar-latitude  38.9140
        calendar-longitude 121.6147
        calendar-location-name "Dalian")
  :hook (after-init . circadian-setup))

;; ========== 平滑滚动 ==========
(use-package pixel-scroll
  :ensure nil
  :hook (after-init . pixel-scroll-precision-mode)
  :custom
  ;; 像素级调整窗口
  (window-resize-pixelwise t)
  (frame-resize-pixelwise t)
  (pixel-scroll-precision-interpolate-page t)
  (pixel-scroll-precision-large-scroll-height 40.0)
  (pixel-scroll-precision-interpolation-factor 30))
#+end_src
*** 字体配置
#+begin_src emacs-lisp
;;; Font setup – optimized (cached + lazy-load)
(defun font-available-p (FONT-NAME)
  "Return non-nil if FONT-NAME is available."
  (find-font (font-spec :name FONT-NAME)))
(defvar my/font-cache nil
  "Cache for detected fonts to avoid repeated system queries.")
(defun my/detect-fonts-once ()
  "Detect required fonts once and cache the results."
  (unless my/font-cache
    (setq my/font-cache
          (list
           :han (cl-loop for f in '("IosevkaTermSS05 Nerd Font Mono + LXGW WenKai Mono Lite Regular"
                                    "LXGW WenKai Mono" "Sarasa Mono SC")
                         when (font-available-p f) return f)
           :symbol (cl-loop for f in '("Symbols Nerd Font Mono")
                            when (font-available-p f) return f)
           :emoji (cl-loop for f in '("Noto Color Emoji" "Apple Color Emoji" "Segoe UI Emoji")
                           when (font-available-p f) return f)))))
(defun my/setup-fonts-beautiful ()
  "Set up fonts (beautified version)."
  (when (display-graphic-p)
    ;; Latin font
    (set-face-attribute 'default nil
                        :font "IosevkaTermSS05 Nerd Font Mono + LXGW WenKai Mono Lite Regular"
                        :height 130
                        :weight 'normal)
    ;; Variable-pitch font (for Org prose)
    (set-face-attribute 'variable-pitch nil
                        :font "IosevkaTermSS05 Nerd Font Mono + LXGW WenKai Mono Lite Regular"
                        :height 1.0)
    ;; Fixed-pitch font (for code blocks)
    (set-face-attribute 'fixed-pitch nil
                        :font "Cascadia Mono"
                        :height 1.0)
    ;; CJK font
    (my/detect-fonts-once)
    (when-let* ((han (plist-get my/font-cache :han)))
      (setq face-font-rescale-alist `((,han . 1.3)))
      (set-fontset-font t 'han (font-spec :family han)))
    ;; Symbols
    (when-let* ((sym (plist-get my/font-cache :symbol)))
      (set-fontset-font t 'symbol (font-spec :family sym) nil 'prepend))
    ;; Emoji
    (when-let* ((emo (plist-get my/font-cache :emoji)))
      (set-fontset-font t 'emoji (font-spec :family emo) nil 'prepend))))
(add-hook 'after-init-hook #'my/setup-fonts-beautiful)
;; Re-apply after new frame (needed for daemon mode)
(add-hook 'after-make-frame-functions #'my/setup-fonts-beautiful)
;; Re-apply after Circadian theme switch
(with-eval-after-load 'circadian
  (add-hook 'circadian-after-load-theme-hook
            (lambda (_theme) (my/setup-fonts-beautiful))))
#+end_src
*** 图标与 Modeline
#+begin_src emacs-lisp
(use-package doom-modeline
  :ensure t
  :hook (after-init . doom-modeline-mode)
  :custom
  ;; ========== 图标设置 ==========
  (doom-modeline-icon t)
  (doom-modeline-major-mode-icon t)
  (doom-modeline-major-mode-color-icon t)
  ;; ========== 高度和样式 ==========
  (doom-modeline-height 28)
  (doom-modeline-bar-width 4)
  (doom-modeline-window-width-limit 85)  ;; 窗口宽度限制
  ;; ========== 显示内容优化 ==========
  (doom-modeline-buffer-file-name-style 'relative-from-project)
  (doom-modeline-buffer-encoding nil)  ;; 隐藏编码
  (doom-modeline-indent-info nil)      ;; 隐藏缩进信息
  (doom-modeline-minor-modes nil)      ;; 隐藏次要模式（推荐）
  ;; ========== 时间与状态 ==========
  (doom-modeline-time-icon t)
  (doom-modeline-time t)             ;; 是否显示时间（可选）
  (doom-modeline-modal t)
  (doom-modeline-modal-icon t)
  (doom-modeline-modal-modern-icon t)
  ;; ========== LSP 支持 ==========
  (doom-modeline-lsp t)
  (doom-modeline-lsp-icon t)
  ;; ========== Git 信息 ==========
  (doom-modeline-vcs-max-length 20)
  (doom-modeline-check-simple-format t)
  (doom-modeline-persp-name t)         ;; 显示 workspace 名称
  (doom-modeline-persp-icon t)
  ;; ========== 性能优化 ==========
  (doom-modeline-env-version nil)      ;; 不显示语言版本
  (doom-modeline-github nil)           ;; 不查询 GitHub
  (doom-modeline-mu4e nil)
  (doom-modeline-irc nil)
  (doom-modeline-gnus nil)
  ;; ========== Org 模式 ==========
  (doom-modeline-enable-word-count t)
  (doom-modeline-continuous-word-count-modes
   '(markdown-mode gfm-mode org-mode text-mode))
  ;; ========== 项目检测 ==========
  (doom-modeline-project-detection 'auto)
  ;; ========== Buffer 状态 ==========
  (doom-modeline-buffer-state-icon t)
  (doom-modeline-buffer-modification-icon t)
  :config
  ;; ========== 自定义 Face ==========
  (custom-set-faces
   ;; 高亮元素
   '(doom-modeline-buffer-modified ((t (:foreground "#bf616a" :weight bold))))
   '(doom-modeline-buffer-major-mode ((t (:foreground "#88c0d0" :weight bold))))
   '(doom-modeline-buffer-path ((t (:foreground "#81a1c1" :weight normal))))
   ;; Git 信息
   '(doom-modeline-vcs-branch ((t (:foreground "#a3be8c"))))
   '(doom-modeline-vcs-modified ((t (:foreground "#ebcb8b"))))
   ;; LSP 状态
   '(doom-modeline-lsp-success ((t (:foreground "#a3be8c" :weight bold))))
   '(doom-modeline-lsp-warning ((t (:foreground "#ebcb8b" :weight bold))))
   '(doom-modeline-lsp-error ((t (:foreground "#bf616a" :weight bold))))
   ;; Bar
   '(doom-modeline-bar ((t (:background "#88c0d0"))))
   '(doom-modeline-bar-inactive ((t (:background "#4c566a")))))
  ;; ========== 时间格式（如果启用）==========
  (when doom-modeline-time
    (setq display-time-format "%H:%M"
          display-time-default-load-average nil
          display-time-interval 60)))
(with-eval-after-load 'circadian
  (defun my/doom-modeline-adapt-theme ()
    "根据主题自动调整 modeline 颜色"
    (let ((bg (face-background 'default))
          (fg (face-foreground 'default)))
      (custom-set-faces
       `(mode-line ((t (:background ,bg :foreground ,fg
                        :box (:line-width 3 :color ,bg)))))
       `(mode-line-inactive ((t (:background ,(doom-blend bg fg 0.1)
                                 :foreground ,(doom-blend fg bg 0.4))))))))
  (add-hook 'circadian-after-load-theme-hook #'my/doom-modeline-adapt-theme))
  (use-package nerd-icons
    :ensure t
    :defer t)  ;; 只在需要时加载（如 dired、ibuffer）
#+end_src
*** 高亮与反馈
**** 当前行高亮
#+begin_src emacs-lisp
(use-package hl-line
  :ensure nil
  :hook ((after-init . global-hl-line-mode)
         ((dashboard-mode eshell-mode shell-mode term-mode vterm-mode) .
          (lambda () (setq-local global-hl-line-mode nil))))
  :custom-face
  ;; 根据主题调整高亮行颜色
  (hl-line ((t (:extend t :background "#2d3436")))))
#+end_src
**** 括号匹配
#+begin_src emacs-lisp
(use-package paren
  :defer t
  :after (:any text prog)
  :hook ((text-mode prog-mode) . show-paren-mode)
  :custom
  (show-paren-when-point-inside-paren t)
  (show-paren-when-point-in-periphery t)
  :config
  (setq show-paren-context-when-offscreen
        (if (childframe-workable-p) 'child-frame 'overlay))
  (with-no-warnings
    (defun display-line-overlay (pos str &optional face)
      "Display line at POS as STR with FACE."
      (let ((ol (save-excursion
                  (goto-char pos)
                  (make-overlay (line-beginning-position)
                                (line-end-position)))))
        (overlay-put ol 'display str)
        (overlay-put ol 'face (or face '(:inherit highlight)))
        ol))
    (defvar-local show-paren--off-screen-overlay nil)
    (defun show-paren-off-screen (&rest _args)
      "Display matching line for off-screen paren."
      (when (overlayp show-paren--off-screen-overlay)
        (delete-overlay show-paren--off-screen-overlay))
      (when (and (overlay-buffer show-paren--overlay)
                 (not (or cursor-in-echo-area
                          executing-kbd-macro
                          noninteractive
                          (minibufferp)
                          this-command))
                 (and (not (bobp))
                      (memq (char-syntax (char-before)) '(?\) ?\$)))
                 (= 1 (logand 1 (- (point)
                                   (save-excursion
                                     (forward-char -1)
                                     (skip-syntax-backward "/\\")
                                     (point))))))
        (cl-letf (((symbol-function #'minibuffer-message)
                   (lambda (msg &rest args)
                     (let ((msg (apply #'format-message msg args)))
                       (setq show-paren--off-screen-overlay
                             (display-line-overlay
                              (window-start) msg))))))
          (blink-matching-open))))
    (advice-add #'show-paren-function :after #'show-paren-off-screen)))
#+end_src
**** 脉冲高亮
#+begin_src emacs-lisp
(use-package pulse
  :ensure nil
  :custom-face
  (pulse-highlight-start-face ((t (:inherit region :background unspecified))))
  (pulse-highlight-face ((t (:inherit region :background unspecified :extend t))))
  :hook (((dumb-jump-after-jump imenu-after-jump) . my/recenter-and-pulse)
         ((bookmark-after-jump magit-diff-visit-file next-error) . my/recenter-and-pulse-line))
  :init
  (with-no-warnings
    (defun my/pulse-momentary-line (&rest _)
      "Pulse the current line."
      (pulse-momentary-highlight-one-line (point)))

    (defun my/pulse-momentary (&rest _)
      "Pulse the region or current line."
      (if (fboundp 'xref-pulse-momentarily)
          (xref-pulse-momentarily)
        (my/pulse-momentary-line)))

    (defun my/recenter-and-pulse (&rest _)
      "Recenter and pulse the region or current line."
      (recenter)
      (my/pulse-momentary))

    (defun my/recenter-and-pulse-line (&rest _)
      "Recenter and pulse the current line."
      (recenter)
      (my/pulse-momentary-line))

    (dolist (cmd '(recenter-top-bottom
                   other-window switch-to-buffer
                   aw-select toggle-window-split
                   windmove-do-window-select
                   pager-page-down pager-page-up
                   treemacs-select-window))
      (advice-add cmd :after #'my/pulse-momentary-line))

    (dolist (cmd '(pop-to-mark-command
                   pop-global-mark
                   goto-last-change))
      (advice-add cmd :after #'my/recenter-and-pulse))))
#+end_src
** 编辑器设置
#+begin_src emacs-lisp
(use-package emacs
  :custom
  ;; Frame 设置
  (frame-inhibit-implied-resize t)
  (frame-resize-pixelwise t)
  ;; UI 元素
  (menu-bar-mode nil)
  (scroll-bar-mode nil)
  (tool-bar-mode nil)
  (inhibit-startup-screen t)
  (use-dialog-box nil)
  ;; 窗口分隔线
  (window-divider-default-right-width 1)
  (window-divider-default-bottom-width 1)
  (window-divider-default-places t)
  ;; 编辑行为
  (delete-selection-mode t)
  (electric-indent-mode nil)
  (electric-pair-mode t)
  (global-auto-revert-mode t)
  ;; 光标和滚动
  (blink-cursor-mode nil)
  (mouse-wheel-progressive-speed nil)
  (scroll-conservatively 101)
  (scroll-step 2)
  (scroll-margin 2)
  ;; 制表符
  (tab-width 4)
  (indent-tabs-mode nil)
  (tab-always-indent 'complete)
  ;; 备份和锁文件
  (make-backup-files nil)
  (create-lockfiles nil)
  ;; 补全
  (completion-cycle-threshold 3)
  ;; 初始模式
  (initial-major-mode 'lisp-interaction-mode)
  :init
  ;; 运行时目录（避免重复创建）
  (let ((dirs '("etc/" "etc/server/" "etc/auto-save-list/"
                "etc/org-persist/" "etc/transient/" "etc/persistent-scratch/")))
    (dolist (dir dirs)
      (let ((full-dir (locate-user-emacs-file dir)))
        (unless (file-exists-p full-dir)
          (make-directory full-dir t)))))
  ;; 路径设置
  (setq server-auth-dir (locate-user-emacs-file "etc/server/")
        auto-save-list-file-prefix (locate-user-emacs-file "etc/auto-save-list/.saves-")
        custom-file (locate-user-emacs-file "etc/custom-vars.el")
        default-directory "C:/Users/wei/Documents/")
  ;; 加载 custom-file
  (load custom-file 'noerror 'nomessage)
  ;; 警告和编译
  (setq warning-minimum-level :emergency
        native-comp-async-report-warnings-errors nil)
  ;; Frame 标题
  (setq frame-title-format "%b"
        icon-title-format "%b")
  ;; 性能优化
  (setq-default bidi-paragraph-direction 'left-to-right)
  (setq bidi-inhibit-bpa t
        load-prefer-newer t
        inhibit-compacting-font-caches t
        display-raw-bytes-as-hex t
        redisplay-skip-fontification-on-input t)
  ;; 滚动优化
  (setq hscroll-step 2
        hscroll-margin 2
        scroll-preserve-screen-position 'always
        auto-hscroll-mode 'current-line
        auto-window-vscroll nil)
  ;; 剪贴板
  (setq select-enable-primary t
        select-enable-clipboard t
        mouse-yank-at-point t)
  ;; 其他
  (setq ring-bell-function 'ignore
        fill-column 80)
  ;; 补全谓词
  (when (boundp 'read-extended-command-predicate)
    (setq read-extended-command-predicate
          #'command-completion-default-include-p))
  ;; 启用被禁用的命令
  (dolist (cmd '(narrow-to-defun narrow-to-page narrow-to-region
                                 dired-find-alternate-file list-timers list-threads))
    (put cmd 'disabled nil))
  ;; 字体大小
  (set-face-attribute 'default nil :height 110)
  ;; _ 作为单词组成部分
  (add-hook 'after-change-major-mode-hook
            (lambda ()
              (modify-syntax-entry ?_ "w")))
  :config
  ;; 窗口分隔线
  (window-divider-mode 1)
  (let ((bg (face-attribute 'default :background)))
    (set-face-attribute 'window-divider nil :foreground bg)
    (set-face-attribute 'window-divider-first-pixel nil :foreground bg)
    (set-face-attribute 'window-divider-last-pixel nil :foreground bg))
  ;; help-fns 延迟加载
  (with-eval-after-load 'help-fns
    (put 'help-fns-edit-variable 'disabled nil))
  :bind
  ([escape] . keyboard-escape-quit))
(use-package winner
  :ensure nil
  :commands (winner-undo winner-redo)
  :hook (after-init . winner-mode)
  :custom (winner-boring-buffers '("*Completions*"
                                   "*Compile-Log*"
                                   "*inferior-lisp*"
                                   "*Fuzzy Completions*"
                                   "*Apropos*"
                                   "*Help*"
                                   "*cvs*"
                                   "*Buffer List*"
                                   "*Ibuffer*"
                                   "*esh command on file*")))
#+end_src

** 编辑体验增强
*** Simple 模式增强
#+begin_src emacs-lisp
(use-package simple
  :ensure nil
  :hook ((after-init . size-indication-mode)
         (text-mode . visual-line-mode)
         ((prog-mode markdown-mode conf-mode) . enable-trailing-whitespace))
  :config
  (setq column-number-mode t
        line-number-mode t
        line-move-visual nil
        track-eol t
        set-mark-command-repeat-pop t)
  (setq-default show-trailing-whitespace nil)
  (defun enable-trailing-whitespace ()
    "Show trailing spaces and delete on saving."
    (setq show-trailing-whitespace t)
    (add-hook 'before-save-hook #'delete-trailing-whitespace nil t))
  ;; 美化进程列表
  (with-no-warnings
    (defun my/list-processes--prettify ()
      "Prettify process list."
      (when-let* ((entries tabulated-list-entries))
        (setq tabulated-list-entries nil)
        (dolist (p (process-list))
          (when-let* ((val (cadr (assoc p entries)))
                      (name (aref val 0))
                      (pid (aref val 1))
                      (status (aref val 2))
                      (status (list status
                                    'face
                                    (if (memq status '(stop exit closed failed))
                                        'error
                                      'success)))
                      (buf-label (aref val 3))
                      (tty (list (aref val 4) 'face 'font-lock-doc-face))
                      (thread (list (aref val 5) 'face 'font-lock-doc-face))
                      (cmd (list (aref val 6) 'face 'completions-annotations)))
            (push (list p (vector name pid status buf-label tty thread cmd))
                  tabulated-list-entries)))))
    (advice-add #'list-processes--refresh :after #'my/list-processes--prettify)))
;; 简化确认
(setq use-short-answers t)
(setq y-or-n-p-use-read-key t
      read-char-choice-use-read-key t)
(setq visible-bell t
      inhibit-compacting-font-caches t
      delete-by-moving-to-trash t
      make-backup-files nil
      auto-save-default nil
      uniquify-buffer-name-style 'post-forward-angle-brackets
      adaptive-fill-regexp "[ t]+|[ t]*([0-9]+.|*+)[ t]*"
      adaptive-fill-first-line-regexp "^* *$"
      sentence-end "\\([。！？]\\|……\\|[.?!][]\"')}]*\\($\\|[ \t]\\)\\)[ \t\n]*"
      sentence-end-double-space nil
      word-wrap-by-category t)
#+end_src
*** 删除选中文本
#+begin_src emacs-lisp
(use-package delsel
  :ensure nil
  :hook (after-init . delete-selection-mode))
#+end_src
*** 跳转 (Avy)
#+begin_src emacs-lisp
(use-package avy
  :defer t
  :ensure t
  :bind (("C-:" . avy-goto-char)
         ("C-\"" . avy-goto-char-2))
  ;;:hook (after-init . avy-setup-default)
  :config
  (setq avy-all-windows nil
        avy-all-windows-alt t
        avy-background t
        avy-style 'pre))
#+end_src
*** 自动配对
#+begin_src emacs-lisp
(use-package elec-pair
  :ensure nil
  :hook (after-init . electric-pair-mode)
  :config
  (setq electric-pair-inhibit-predicate 'electric-pair-conservative-inhibit))
#+end_src
*** 智能删除空白
#+begin_src emacs-lisp
(use-package hungry-delete
  :ensure t
  :diminish
  :hook (after-init . global-hungry-delete-mode)
  :init
  (setq hungry-delete-chars-to-skip " \t\f\v"
        hungry-delete-except-modes
        '(help-mode minibuffer-mode minibuffer-inactive-mode calc-mode)))
#+end_src
*** 智能行首行尾移动
#+begin_src emacs-lisp
  (use-package mwim
    :ensure t
    :bind (([remap move-beginning-of-line] . mwim-beginning)
           ([remap move-end-of-line] . mwim-end)))
#+end_src
*** 长行处理
#+begin_src emacs-lisp
  (use-package so-long
    :ensure t
    :hook (after-init . global-so-long-mode))
#+end_src
** 补全、搜索与命令面板
*** Orderless Vertico Marginalia
#+begin_src emacs-lisp
(use-package orderless
  :ensure t
  :after vertico
  :defer t
  :custom
  (completion-styles '(orderless basic))
  (completion-category-overrides '((file (styles basic partial-completion))))
  (completion-category-defaults nil) ;; Disable defaults, use our settings
  (orderless-component-separator #'orderless-escapable-split-on-space))

(use-package pinyinlib
  :ensure t
  :after orderless
  :autoload pinyinlib-build-regexp-string
  :init
  (defun completion--regex-pinyin (str)
    (orderless-regexp (pinyinlib-build-regexp-string str)))
  (add-to-list 'orderless-matching-styles 'completion--regex-pinyin))

(use-package vertico
  :ensure t
  :bind (:map vertico-map
         ("RET" . vertico-directory-enter)
         ("DEL" . vertico-directory-delete-char)
         ("M-DEL" . vertico-directory-delete-word))
  :hook ((after-init . vertico-mode)
         (rfn-eshadow-update-overlay . vertico-directory-tidy))
  :custom
  (vertico-resize t)
  (vertico-cycle t)
  (vertico-resize nil)
  (vertico-count 15))

(use-package marginalia
  :ensure t
  :after vertico
  :hook (after-init . marginalia-mode)
  :config
  (defun my/marginalia-annotate-command (cand)
    "Annotate command CAND with its documentation string."
    (when-let* ((sym (intern-soft cand)))
      (concat
       (when (member sym minor-mode-list)
         (if (and (boundp sym) (symbol-value sym))
             (propertize " (On)" 'face 'marginalia-on)
           (propertize " (Off)" 'face 'marginalia-off)))
       (marginalia-annotate-binding cand)
       (marginalia--documentation (marginalia--function-doc sym)))))
  (setf (alist-get 'command marginalia-annotator-registry)
        '(my/marginalia-annotate-command marginalia-annotate-binding builtin none)))
#+end_src

*** Consult & Embark
#+begin_src emacs-lisp
  (use-package consult-dir
    :ensure t
    :after consult)
  (use-package consult
    :ensure t
    :hook (completion-list-mode . consult-preview-at-point-mode)
    :defer t
    :bind (([remap imenu] . consult-imenu)
           ([remap isearch-forward] . consult-line)
           ([remap isearch-backward] . consult-line)
           ([remap find-file-read-only] . consult-recent-file)
           ([remap list-directory] . consult-dir)
           ([remap goto-line] . consult-goto-line)
           ([remap bookmark-jump] . consult-bookmark)
           ([remap recentf-open-files] . consult-recent-file)
           ([remap repeat-complex-command] . consult-complex-command)
           ([remap jump-to-register] . consult-register-load)
           ([remap point-to-register] . consult-register-store))
    :config
    (with-eval-after-load 'xref
      (setq xref-show-xrefs-function #'consult-xref
            xref-show-definitions-function #'consult-xref))

    (defvar consult-colors-history nil)
    (autoload 'list-colors-duplicates "facemenu")
    (autoload 'consult--read "consult")

    (defun consult-colors-emacs (color)
      "Show a list of all supported colors."
      (interactive
       (list (consult--read (list-colors-duplicates (defined-colors))
                            :prompt "Emacs color: "
                            :require-match t
                            :category 'color
                            :history '(:input consult-colors-history))))
      (insert color))

    (defun consult-colors--web-list ()
      "Return list of CSS colors."
      (require 'shr-color)
      (sort (mapcar #'downcase (mapcar #'car shr-color-html-colors-alist))
            #'string-lessp))

    (defun consult--orderless-regexp-compiler (input type &rest _config)
      (setq input (orderless-pattern-compiler input))
      (cons
       (mapcar (lambda (r) (consult--convert-regexp r type)) input)
       (lambda (str) (orderless--highlight input str))))

    (defun consult-colors-web (color)
      "Show a list of all CSS colors."
      (interactive
       (list (consult--read (consult-colors--web-list)
                            :prompt "Color: "
                            :require-match t
                            :category 'color
                            :history '(:input consult-colors-history))))
      (insert color))

    (with-no-warnings
      (consult-customize consult-ripgrep consult-fd
                         consult-bookmark consult-goto-line consult-theme
                         consult-recent-file
                         consult-buffer
                         :preview-key '(:debounce 0.2 any)))

    (setq register-preview-delay 0.5
          register-preview-function #'consult-register-format)

    :custom
    (consult-fontify-preserve nil)
    (consult-async-min-input 2)
    (consult-async-refresh-delay 0.15)
    (consult-async-input-throttle 0.2)
    (consult-async-input-debounce 0.1))

  (use-package embark
    :ensure t
    :bind (([remap describe-bindings] . embark-bindings)
           :map minibuffer-local-map
           ("M-o" . embark-act)
           ("C-c C-c" . embark-export)
           ("M-." . my/embark-preview)
           ("C-c C-o" . embark-collect))
    :init
    (setq prefix-help-command #'embark-prefix-help-command)
    :config
    (defun my/embark-preview ()
      "Previews candidate in vertico buffer."
      (interactive)
      (unless (bound-and-true-p consult--preview-function)
        (save-selected-window
          (let ((embark-quit-after-action nil))
            (embark-dwim)))))
    (add-to-list 'display-buffer-alist
                 '("\\`\\*Embark Collect \\(Live\\|Completions\\)\\*"
                   nil
                   (window-parameters (mode-line-format . none))))
    (with-eval-after-load 'which-key
      (defun embark-which-key-indicator ()
        "An embark indicator that displays keymaps using which-key."
        (lambda (&optional keymap targets prefix)
          (if (null keymap)
              (which-key--hide-popup-ignore-command)
            (which-key--show-keymap
             (if (eq (plist-get (car targets) :type) 'embark-become)
                 "Become"
               (format "Act on %s '%s'%s"
                       (plist-get (car targets) :type)
                       (embark--truncate-target (plist-get (car targets) :target))
                       (if (cdr targets) "…" "")))
             (if prefix
                 (pcase (lookup-key keymap prefix 'accept-default)
                   ((and (pred keymapp) km) km)
                   (_ (key-binding prefix 'accept-default)))
               keymap)
             nil nil t (lambda (binding)
                         (not (string-suffix-p "-argument" (cdr binding))))))))
      (setq embark-indicators
            '(embark-which-key-indicator
              embark-highlight-indicator
              embark-isearch-highlight-indicator))
      (defun embark-hide-which-key-indicator (fn &rest args)
        "Hide which-key indicator immediately."
        (which-key--hide-popup-ignore-command)
        (let ((embark-indicators
               (remq #'embark-which-key-indicator embark-indicators)))
          (apply fn args)))
      (advice-add #'embark-completing-read-prompter
                  :around #'embark-hide-which-key-indicator)))
  (use-package embark-consult
    :ensure t
    :after embark
    :bind (:map minibuffer-mode-map
           ("C-c C-o" . embark-export))
    :hook (embark-collect-mode . consult-preview-at-point-mode))
#+end_src
*** 自动补全
#+begin_src emacs-lisp
(use-package corfu
  :ensure t
  :defer t
  :autoload (corfu-quit consult-completion-in-region)
  :functions (persistent-scratch-save corfu-move-to-minibuffer)
  :init
  (setq corfu-auto t
        corfu-auto-prefix 2
        corfu-auto-delay 0.2
        corfu-count 12
        corfu-left-margin-width 2
        corfu-preview-current nil
        corfu-on-exact-match nil
        corfu-popupinfo-delay '(0.4 . 0.2)
        text-mode-ispell-word-completion nil
        ispell-alternate-dictionary nil)
  :config
  (setq global-corfu-modes '((not erc-mode circe-mode help-mode
                                  gud-mode vterm-mode) t))
  :custom-face
  (corfu-border ((t (:inherit region :background unspecified))))
  :bind (:map corfu-map
         ("<escape>" . corfu-quit)
         ("TAB" . corfu-next)
         ([tab] . corfu-next)
         ("RET" . corfu-complete)
         ("S-TAB" . corfu-previous)
         ([backtab] . corfu-previous))
  :hook ((after-init . global-corfu-mode)
         (global-corfu-mode . corfu-popupinfo-mode)
         (global-corfu-mode . corfu-history-mode))
  :config
  ;;Quit completion before saving
  (add-hook 'before-save-hook #'corfu-quit)
  (advice-add #'persistent-scratch-save :before #'corfu-quit)
  ;; Move completions to minibuffer
  (defun corfu-move-to-minibuffer ()
    (interactive)
    (pcase completion-in-region--data
      (`(,beg ,end ,table ,pred ,extras)
       (let ((completion-extra-properties extras)
             completion-cycle-threshold completion-cycling)
         (consult-completion-in-region beg end table pred)))))
  (add-to-list 'corfu-continue-commands #'corfu-move-to-minibuffer))
(use-package nerd-icons-corfu
  :ensure t
  :autoload nerd-icons-corfu-formatter
  :after corfu
  :init
  (add-to-list 'corfu-margin-formatters #'nerd-icons-corfu-formatter))
;; Add extensions
(use-package cape
  :ensure t
  :commands (cape-file cape-elisp-block cape-keyword cape-dabbrev)
  :init
  (add-to-list 'completion-at-point-functions #'cape-dabbrev)
  (add-to-list 'completion-at-point-functions #'cape-file)
  (add-to-list 'completion-at-point-functions #'cape-keyword))
(use-package yasnippet
  :ensure t
  :diminish
  :hook (((LaTeX-mode python-ts-mode ess-r-mode poly-markdown+r-mode) . yas-minor-mode)
         (post-self-insert . my/yas-try-expanding-auto-snippets))
  :config
  (yas-reload-all)
  (with-eval-after-load 'warnings
    (cl-pushnew '(yasnippet backquote-change)
                warning-suppress-types :test 'equal))

  (setq yas-triggers-in-field t)
  (defun my/yas-try-expanding-auto-snippets ()
    (when (and (boundp 'yas-minor-mode) yas-minor-mode)
      (let ((yas-buffer-local-condition ''(require-snippet-condition . auto)))
        (yas-expand))))
  :custom
  (yas-snippet-dirs (list (locate-user-emacs-file "yasnippets"))))
(use-package yasnippet-capf
  :ensure t
  :after cape
  :commands yasnippet-capf
  :functions cape-capf-super eglot-completion-at-point my/eglot-capf-with-yasnippet
  :init
  (add-to-list 'completion-at-point-functions #'yasnippet-capf)
  (defun my/eglot-capf-with-yasnippet ()
    (setq-local completion-at-point-functions
                (list
	             (cape-capf-super
		          #'eglot-completion-at-point
		          #'yasnippet-capf))))
  (add-hook 'eglot-managed-mode-hook #'my/eglot-capf-with-yasnippet))
#+end_src
** 项目文件与工具
*** Ibuffer
#+begin_src emacs-lisp
(use-package ibuffer
  :ensure nil
  :bind ("C-x C-b" . ibuffer)
  :init (setq ibuffer-filter-group-name-face '(:inherit (font-lock-string-face bold))))
(use-package nerd-icons-ibuffer
  :ensure t
  :after ibuffer
  :hook (ibuffer-mode . nerd-icons-ibuffer-mode)
  :config (setq nerd-icons-ibuffer-icon t))
#+end_src
*** Minibuffer
#+begin_src emacs-lisp
(use-package minibuffer
  :bind (:map minibuffer-local-map
         ([escape] . abort-recursive-edit)
         :map minibuffer-local-ns-map
         ([escape] . abort-recursive-edit)
         :map minibuffer-local-completion-map
         ([escape] . abort-recursive-edit)
         :map minibuffer-local-must-match-map
         ([escape] . abort-recursive-edit)
         :map minibuffer-local-isearch-map
         ([escape] . abort-recursive-edit))
  :custom
  (completion-auto-help t)
  (completion-show-help nil)
  (completion-cycle-threshold nil)
  (completion-auto-select 'second-tab)
  (enable-recursive-minibuffers t)
  (minibuffer-depth-indicate-mode t)
  (minibuffer-default-prompt-format " [%s]")
  (minibuffer-electric-default-mode t)
  (minibuffer-completion-auto-choose nil)
  (minibuffer-follows-selected-frame nil)
  (completion-ignore-case t)
  (read-buffer-completion-ignore-case t)
  (read-file-name-completion-ignore-case t)
  (completion-styles '(basic partial-completion substring flex))
  (completion-category-overrides
   '((buffer (styles . (flex)))
     (imenu (styles . (substring)))))
  (completions-format 'one-column)
  (completions-max-height 13)
  (completions-detailed t))
#+end_src
*** Dired
#+begin_src emacs-lisp
(use-package dired
  :ensure nil
  :bind (:map dired-mode-map
         ("C-c C-p" . wdired-change-to-wdired-mode))
  :custom
  ;; 智能目标（自动选择另一个窗口的目录）
  (dired-dwim-target t)
  ;; 递归操作
  (dired-recursive-deletes 'always)
  (dired-recursive-copies 'always)
  ;; 列表显示选项
  (dired-listing-switches "-alh --group-directories-first")
  ;; 性能优化
  (dired-hide-details-mode nil)
  (dired-kill-when-opening-new-dired-buffer t) ; Emacs 28+
  :hook
  ;; 自动刷新 dired 缓冲区
  (dired-mode . auto-revert-mode)
  :config
  ;; Windows 兼容性（如果 ls 不可用）
  (when (and (eq system-type 'windows-nt)
             (not (executable-find "ls")))
    (setq dired-listing-switches "-alh")))
(use-package diredfl
  :ensure t
  :hook (dired-mode . diredfl-mode))
(use-package nerd-icons-dired
  :ensure t
  :diminish
  :custom-face
  (nerd-icons-dired-dir-face ((t (:inherit nerd-icons-dsilver :foreground unspecified))))
  :hook (dired-mode . nerd-icons-dired-mode))
(use-package fd-dired
  :ensure t
  :when (executable-find "fd")
  :bind (:map dired-mode-map
         ("C-c f" . fd-dired)))
#+end_src
*** Bookmark
#+begin_src emacs-lisp
(use-package bookmark
  :ensure nil
  :config
  (setq bookmark-default-file (locate-user-emacs-file "etc/bookmarks"))
  (with-no-warnings
    (defun my/bookmark-bmenu--revert ()
      "Re-populate tabulated-list-entries."
      (let (entries)
        (dolist (full-record (bookmark-maybe-sort-alist))
          (let* ((name (bookmark-name-from-record full-record))
                 (annotation (bookmark-get-annotation full-record))
                 (location (bookmark-location full-record))
                 (file (file-name-nondirectory location))
                 (type (let ((fmt "%-8.8s"))
                         (cond ((null location)
                                (propertize (format fmt "NOFILE") 'face 'warning))
                               ((file-remote-p location)
                                (propertize (format fmt "REMOTE") 'face 'mode-line-buffer-id))
                               ((not (file-exists-p location))
                                (propertize (format fmt "NOTFOUND") 'face 'error))
                               ((file-directory-p location)
                                (propertize (format fmt "DIRED") 'face 'warning))
                               (t (propertize (format fmt "FILE") 'face 'success))))))
            (push (list
                   full-record
                   `[,(if (and annotation (not (string-equal annotation "")))
                          "*" "")
                     ,icon
                     ,(if (display-mouse-p)
                          (propertize name
                                      'font-lock-face 'bookmark-menu-bookmark
                                      'mouse-face 'highlight
                                      'follow-link t
                                      'help-echo "mouse-2: go to this bookmark in other window")
                        name)
                     ,type
                     ,@(if bookmark-bmenu-toggle-filenames
                           (list (propertize location 'face 'completions-annotations)))])
                  entries)))
        (tabulated-list-init-header)
        (setq tabulated-list-entries entries))
      (tabulated-list-print t))
    (advice-add #'bookmark-bmenu--revert :override #'my/bookmark-bmenu--revert)

    (defun my/bookmark-bmenu-list ()
      "Display a list of existing bookmarks."
      (interactive)
      (bookmark-maybe-load-default-file)
      (let ((buf (get-buffer-create bookmark-bmenu-buffer)))
        (if (called-interactively-p 'interactive)
            (pop-to-buffer buf)
          (set-buffer buf)))
      (bookmark-bmenu-mode)
      (bookmark-bmenu--revert))
    (advice-add #'bookmark-bmenu-list :override #'my/bookmark-bmenu-list)
    (define-derived-mode bookmark-bmenu-mode tabulated-list-mode "Bookmark Menu"
      (setq truncate-lines t)
      (setq buffer-read-only t)
      (setq tabulated-list-format
            `[("" 1)
              ("" ,(if (icons-displayable-p) 2 0))
              ("Bookmark" ,bookmark-bmenu-file-column bookmark-bmenu--name-predicate)
              ("Type" 9)
              ,@(if bookmark-bmenu-toggle-filenames
                    '(("File" 0 bookmark-bmenu--file-predicate)))])
      (setq tabulated-list-padding bookmark-bmenu-marks-width)
      (setq tabulated-list-sort-key '("Bookmark" . nil))
      (add-hook 'tabulated-list-revert-hook #'bookmark-bmenu--revert nil t)
      (setq revert-buffer-function #'bookmark-bmenu--revert)
      (tabulated-list-init-header))))
#+end_src
*** Ripgrep
#+begin_src emacs-lisp
(use-package rg
  :ensure t
  :hook (after-init . rg-enable-default-bindings)
  :bind (:map rg-global-map
         ("c" . rg-dwim-current-dir)
         ("f" . rg-dwim-current-file)
         ("m" . rg-menu))
  :init
  (setq rg-group-result t
        rg-show-columns t)
  :config
  (cl-pushnew '("tmpl" . "*.tmpl") rg-custom-type-aliases))
#+end_src
** 笔记系统
*** Org 性能优化
#+begin_src emacs-lisp
(defvar my/org-optimized-p nil
  "标记是否已经优化过Org解析。")
(defun my/org-optimize-emphasis-parsing ()
  "一次性设置 emphasis 解析。"
  (unless my/org-optimized-p
    (setq org-emphasis-regexp-components '("-[:space:]('\"{[:nonascii:]"
                                           "-[:space:].,:!?;'\")}\\[[:nonascii:]"
                                           "[:space:]"
                                           "."
                                           1))
    (setq org-match-substring-regexp
          (concat "\\([0-9a-zA-Zα-γΑ-Ω]\\)\\([_^]\\)\\("
                  "\\(?:" (org-create-multibrace-regexp "{" "}" org-match-sexp-depth) "\\)"
                  "\\|"
                  "\\(?:" (org-create-multibrace-regexp "(" ")" org-match-sexp-depth) "\\)"
                  "\\|"
                  "\\(?:\\*\\|[+-]?[[:alnum:].,\\]*[[:alnum:]]\\)\\)"))
    (org-set-emph-re 'org-emphasis-regexp-components org-emphasis-regexp-components)
    (org-element-update-syntax)
    (setq my/org-optimized-p t)))
#+end_src
*** Org-mode 核心配置
#+begin_src emacs-lisp
(use-package org
  :diminish
  :defer t
  :mode ("\\.org\\'" . org-mode)
  :hook ((org-mode . (lambda ()
                       (lazytab-mode 1)
                       (visual-line-mode 1)
                       (turn-on-org-cdlatex)
                       (yas-minor-mode 1)
                       (my/add-latex-in-org-mode-expansions)
                       (my/org-indent-setup)))
         (org-babel-after-execute . org-redisplay-inline-images))
  :bind (("C-c a" . org-agenda)
         :map org-mode-map
         ("C-c i" . yank-media)
         ("C-c l" . org-store-link))
  :custom
  (org-persist-directory (locate-user-emacs-file "etc/org-persist/"))
  (org-directory my/org-agenda-root)
  (org-hide-emphasis-markers t)
  (org-startup-indented t)
  (org-fontify-todo-headline nil)
  (org-fontify-done-headline t)
  (org-fontify-whole-heading-line t)
  (org-fontify-quote-and-verse-blocks t)
  (org-ellipsis " [+]")
  (org-list-demote-modify-bullet
   '(("+" . "-") ("1." . "a.") ("-" . "+")))
  (org-image-actual-width '(600))
  (org-preview-latex-image-directory
   (expand-file-name "orgltxpng/" (getenv "TEMP")))
  (org-latex-preview-numbered t)
  (org-imenu-depth 4)
  (org-clone-delete-id t)
  (org-use-sub-superscripts '{})
  (org-yank-adjusted-subtrees t)
  (org-ctrl-k-protect-subtree 'error)
  (org-fold-catch-invisible-edits 'show-and-error)
  (org-return-follows-link nil)
  (org-todo-keywords
   '((sequence "TODO(t)" "HOLD(h!)" "WIP(i!)" "WAIT(w!)" "|" "DONE(d!)" "CANCELLED(c@/!)")))
  (org-todo-keyword-faces
   '(("TODO" :foreground "#7c7c75" :weight bold)
     ("HOLD" :foreground "#feb24c" :weight bold)
     ("WIP" :foreground "#0098dd" :weight bold)
     ("WAIT" :foreground "#9f7efe" :weight bold)
     ("DONE" :foreground "#50a14f" :weight bold)
     ("CANCELLED" :foreground "#ff6480" :weight bold)))
  (org-use-fast-todo-selection 'expert)
  (org-enforce-todo-dependencies t)
  (org-enforce-todo-checkbox-dependencies t)
  (org-priority-faces
   '((?A :foreground "red") (?B :foreground "orange") (?C :foreground "yellow")))
  (org-global-properties
   '(("EFFORT_ALL" . "0:15 0:30 0:45 1:00 2:00 3:00 4:00 5:00 6:00 7:00 8:00")
     ("APPT_WARNTIME_ALL" . "0 5 10 15 20 25 30 45 60")
     ("STYLE_ALL" . "habit")))
  (org-columns-default-format "%25ITEM %TODO %SCHEDULED %DEADLINE %3PRIORITY %TAGS %CLOCKSUM %EFFORT{:}")
  (org-log-repeat 'time)
  (org-closed-keep-when-no-todo t)
  (org-archive-location "%s_archive::datetree/")
  (org-refile-use-cache nil)
  (org-refile-targets '((org-agenda-files . (:maxlevel . 6))))
  (org-refile-use-outline-path 'file)
  (org-outline-path-complete-in-steps nil)
  (org-refile-allow-creating-parent-nodes 'confirm)
  (org-goto-auto-isearch nil)
  (org-goto-interface 'outline-path-completion)
  (org-use-fast-tag-selection t)
  (org-fast-tag-selection-single-key t)
  (org-id-link-to-org-use-id 'create-if-interactive-and-no-custom-id)
  :config
  (setq org-yank-image-save-method "file")
  (defun my/org-yank-image-file-name ()
    "生成图片保存路径"
    (unless (buffer-file-name)
      (error "请先保存当前 org 文件"))
    (let* ((heading (or (org-get-heading t t t t) "default"))
           (clean-heading (replace-regexp-in-string "[^a-zA-Z0-9\u4e00-\u9fa5-]+" "_" heading))
           (timestamp (format-time-string "%Y%m%d_%H%M%S"))
           (base-dir (file-name-directory (buffer-file-name)))
           (dir (expand-file-name (concat "Figure/" clean-heading "/") base-dir))
           (filename (concat timestamp ".png"))
           (full-path (expand-file-name filename dir)))
      (make-directory dir t)
      (message "保存图片到: %s" full-path)
      full-path))
  (setq org-yank-image-file-name-function #'my/org-yank-image-file-name)
  (defun my/org-yank-image-add-description ()
    "在粘贴图片后添加描述"
    (when (eq major-mode 'org-mode)
      (save-excursion
        ;; 找到刚插入的链接
        (when (re-search-backward "\\[\\[file:\\([^]]+\\)\\]\\]" (line-beginning-position) t)
          (let* ((file-path (match-string 1))
                 (description (read-string "图片描述: ")))
            (when (not (string-empty-p description))
              ;; 替换整个链接，添加描述
              (replace-match (format "[[file:%s][%s]]" file-path description) t t)))))))
  ;; 移除旧的 advice（如果存在）
  (advice-remove 'yank-media #'my/org-yank-image-add-description)
  ;; 添加新的 advice
  (advice-add 'yank-media :after
              (lambda (&rest _)
                (when (eq major-mode 'org-mode)
                  (run-with-idle-timer 0.1 nil #'my/org-yank-image-add-description))))
  (defun my/add-latex-in-org-mode-expansions ()
    (modify-syntax-entry ?\\ "\\" org-mode-syntax-table)
    (setq paragraph-start (concat paragraph-start "\\|\\\\end{\\([A-Za-z0-9*]+\\)}"))
    (setq-local beginning-of-defun-function 'my/org-beginning-of-defun)
    (with-eval-after-load 'expand-region
      (set (make-local-variable 'my/try-expand-list)
           (append (cl-set-difference my/try-expand-list
                                      '(my/mark-method-call
                                        my/mark-inside-pairs
                                        my/mark-outside-pairs))
                   '(LaTeX-mark-environment
                     my/mark-LaTeX-inside-math
                     my/mark-latex-inside-pairs
                     my/mark-latex-outside-pairs
                     my/mark-LaTeX-math)))))
  (defun my/org-indent-setup ()
    (setq-local show-paren-mode nil))
  (my/org-optimize-emphasis-parsing))
#+end_src
*** Denote 笔记系统
#+begin_src emacs-lisp
(use-package denote
  :ensure t
  :defer t
  :custom
  (denote-file-type 'org)
  (denote-directory my/denote-root)
  (denote-signature-prompt nil)
  (denote-known-keywords
   '("literature" "research" "thesis"
     "project" "work" "meeting" "report"
     "journal" "idea" "reading" "personal"
     "todo" "draft" "archive"))
  (denote-infer-keywords t)
  (denote-sort-keywords t)
  (denote-prompts '(title keywords template))
  (denote-save-buffers nil)
  (denote-rename-confirmations '(rewrite-front-matter modify-file-name))
  (denote-link-fontify t)
  (denote-date-prompt-use-org-read-date t)
  (denote-date-format "%Y-%m-%d %a")
  :hook ((org-mode . denote-fontify-links-mode-maybe)
         (dired-mode . denote-dired-mode-in-directories))
  :bind (("C-c n n" . denote)
         ("C-c n R" . denote-rename-file)
         ("C-c n r" . denote-rename-file-keywords)
         ("C-c n d" . denote-dired-rename-files)
         ("C-c n b" . denote-backlinks)
         ("C-c n a" . denote-add-links)
         :map dired-mode-map
         ("C-c C-d C-r" . denote-dired-rename-files)
         ("C-c C-d C-i" . denote-dired-link-marked-notes))
  :config
  (denote-rename-buffer-mode 1)
  (setq denote-templates
        `((报告 . "* 摘要\n\n* 引言\n\n* 主体\n** 方法\n** 结果\n\n* 结论\n\n* 参考文献\n\n")
          (日记 . ,(concat "* 今日总结"
                           "\n\n"
                           "* 待办事项"
                           "\n** 已完成"
                           "\n** 未完成"
                           "\n\n"
                           "* 思考与感悟"
                           "\n\n"))
          (文献 . ,(concat "* 文献信息"
                           "\n** 作者"
                           "\n** 发表时间"
                           "\n** 期刊/会议"
                           "\n\n"
                           "* 主要内容"
                           "\n\n"
                           "* 关键观点"
                           "\n\n"
                           "* 批判性思考"
                           "\n\n"
                           "* 与已有笔记的关联"
                           "\n\n"))
          (草稿 . ,(concat "* 大纲"
                           "\n\n"
                           "* 初稿"
                           "\n\n"
                           "* 修改记录"
                           "\n\n")))))
(use-package consult-denote
  :ensure t
  :after (consult denote)
  :bind (("C-c n f" . consult-denote-find)
         ("C-c n l" . consult-denote-link)
         ("C-c n g" . consult-denote-grep))
  :config
  (setq consult-denote-grep-command #'consult-ripgrep
        denote-consult-directory denote-directory))
#+end_src
*** Org Agenda 配置
#+begin_src emacs-lisp
(defvar my/org-agenda-files-cache nil
  "缓存的 agenda 文件列表")
(defvar my/org-agenda-files-last-check 0
  "上次检查的时间戳")
(defun my/org-auto-collect-agenda-files (&optional force-refresh)
  "自动收集所有子目录中的 Org 文件（带缓存）"
  (let ((now (float-time))
        (cache-timeout 300))
    (when (or force-refresh
              (null my/org-agenda-files-cache)
              (> (- now my/org-agenda-files-last-check) cache-timeout))
      (setq my/org-agenda-files-cache
            (delete-dups
             (append
              (when (file-directory-p my/org-projects-dir)
                (directory-files-recursively my/org-projects-dir "\\.org$"))
              (when (file-directory-p my/org-work-dir)
                (directory-files-recursively my/org-work-dir "\\.org$"))
              (when (file-directory-p my/org-personal-dir)
                (directory-files-recursively my/org-personal-dir "\\.org$"))
              (let ((inbox (expand-file-name "inbox.org" my/org-agenda-root)))
                (when (file-exists-p inbox) (list inbox))))))
      (setq my/org-agenda-files-last-check now))
    my/org-agenda-files-cache))
(with-eval-after-load 'org-agenda
  (setq org-agenda-files nil)  ;; 启动时不设置
  (defun my/org-agenda-prepare (&rest _)
    "延迟加载 agenda 文件列表"
    (unless org-agenda-files
      (setq org-agenda-files (my/org-auto-collect-agenda-files))))
  (advice-add 'org-agenda :before #'my/org-agenda-prepare)
  
  ;; Agenda 显示设置（保持原样）
  (setq org-agenda-span 'week
        org-agenda-start-day nil
        org-agenda-window-setup 'current-window
        org-agenda-include-deadlines t
        org-agenda-show-all-dates nil
        org-agenda-block-separator ?─
        org-agenda-time-grid
        '((daily today require-timed)
          (800 1000 1200 1400 1600 1800 2000)
          " ┄┄┄┄┄ " "┄┄┄┄┄┄┄┄┄┄┄┄┄┄┄")
        org-agenda-current-time-string
        "⭠ now ─────────────────────────────────────────────────"
        org-agenda-prefix-format
        '((agenda . " %i %-12:c %t")
          (todo . " %i %-12:c")
          (tags . " %i %-12:c")
          (search . " %i %-12:c"))
        org-agenda-sorting-strategy
        '((agenda time-up priority-down category-keep)
          (todo priority-down category-keep)
          (tags priority-down category-keep)
          (search category-keep))))

#+end_src
*** Org Capture
#+begin_src emacs-lisp
(defvar my/org-capture-files
  '(("inbox" . "inbox.org")
    ("ideas" . "ideas.org")
    ("notes" . "notes.org")))
(defun my/create-org-capture-file (name)
  "创建指定名称的捕获文件。"
  (let ((file (expand-file-name name my/org-agenda-root)))
    (unless (file-exists-p file)
      (with-temp-file file
        (insert (format "#+TITLE: %s\n#+CREATED: %s\n\n"
                        (file-name-sans-extension name)
                        (format-time-string "%Y-%m-%d %a %H:%M"))))
      (message "已创建捕获文件: %s" (file-relative-name file my/org-agenda-root)))
    file))
(defun my/ensure-capture-files ()
  "确保所有捕获模板文件都存在。"
  (dolist (file-pair my/org-capture-files)
    (my/create-org-capture-file (cdr file-pair))))
(with-eval-after-load 'org-capture
  (my/ensure-capture-files))

(defun my/get-capture-file (key)
  "根据KEY获取对应的捕获文件路径。"
  (let ((file-name (alist-get key my/org-capture-files nil nil #'string=)))
    (when file-name
      (expand-file-name file-name my/org-agenda-root))))

(defun my/org-capture-setup ()
  "配置 capture 缓冲区环境。"
  (setq-local org-complete-tags-always-offer-all-agenda-tags t)
  (visual-line-mode -1)
  (auto-fill-mode -1))

(defun my/org-capture-selection-or-block ()
  "根据选中内容返回适当格式。"
  (with-current-buffer (org-capture-get :original-buffer)
    (let ((content (plist-get org-store-link-plist :initial))
          (src-mode (replace-regexp-in-string "-mode$" "" (symbol-name major-mode)))
          (is-code (derived-mode-p 'prog-mode)))
      (if (string-empty-p (or content ""))
          ""
        (if is-code
            (format "\n#+begin_src %s\n%s\n#+end_src\n" src-mode content)
          (format "\n#+begin_quote\n%s\n#+end_quote\n" content))))))
(defun my/org-capture-link-format ()
  "格式化链接或标注信息。"
  (with-current-buffer (org-capture-get :original-buffer)
    (let* ((annotation (plist-get org-store-link-plist :annotation))
           (file (buffer-file-name)))
      (cond
       ((string-empty-p (or annotation "")) "")
       ((eq major-mode 'org-mode)
        (concat "- reference :: "
                (replace-regexp-in-string ".*::\\s-*\\([^]]+\\)\\s-*\\(.*\\)" "\\1" annotation)))
       (file
        (format "- reference :: %s[[%s]]" annotation (file-name-nondirectory file)))
       (t (format "- reference :: %s" annotation))))))
(use-package org-capture
  :bind ("C-c c" . org-capture)
  :hook (org-capture-mode . my/org-capture-setup)
  :config
  (setq org-capture-templates
        `(("t" "Todo" entry (file ,(my/get-capture-file "inbox"))
           "* TODO %?\n%U\n%i\n"
           :empty-lines 1)
          ("n" "Notes" entry (file+headline ,(my/get-capture-file "notes") "Notes")
           "* %?\n%(my/org-capture-selection-or-block)\n%(my/org-capture-link-format)\n%U"
           :prepend t :empty-lines 1)
          ("i" "Idea" entry (file+headline ,(my/get-capture-file "ideas") "Ideas")
           "* %^{Title}\n%?\n%(my/org-capture-selection-or-block)\n%(my/org-capture-link-format)\n%U"
           :prepend t :empty-lines 1)))
  (defun my/org-capture-refresh-agenda (&rest _)
    "捕获完成后自动刷新 agenda。"
    (when (get-buffer "*Org Agenda*")
      (with-current-buffer "*Org Agenda*"
        (org-agenda-redo))))
  (advice-add 'org-capture-finalize :after #'my/org-capture-refresh-agenda))
#+end_src
*** Org Modern
#+begin_src emacs-lisp
(use-package org-modern
  :ensure t
  :defer t ; 完全延迟加载，直到 org-mode 触发
  :hook
  ((org-mode . org-modern-mode)
   (org-agenda-finalize . org-modern-agenda))
  :config
  ;; 自定义 face（根据主题调整）
  (use-package org-modern
    :ensure t
    :hook (org-mode . org-modern-mode)
    :custom
    (org-tags-column 0)
    (org-special-ctrl-a/e t)
    (org-insert-heading-respect-content t)
    (org-modern-keyword t)
    (org-modern-table-horizontal 0.2)
    (org-modern-tag t)
    (org-modern-timestamp nil)
    (org-modern-table t)
    (org-modern-priority t)
    :config
    (custom-set-faces
     '(org-modern-symbol ((t (:height 1.0 :foreground "#81A1C1"))))
     '(org-modern-tag ((t (:background "#3B4252" :foreground "#ECEFF4"
                           :height 1.0 :weight regular :box nil))))
     '(org-modern-priority ((t (:height 1.0 :inherit org-priority))))
     '(org-modern-date-active ((t (:background "#434C5E" :foreground "#88C0D0"
                                   :height 1.0 :box nil))))
     '(org-modern-date-inactive ((t (:background "#3B4252" :foreground "#4C566A"
                                     :height 1.0 :box nil))))
     '(org-modern-time-active ((t (:inherit org-modern-date-active))))
     '(org-modern-time-inactive ((t (:inherit org-modern-date-inactive)))))
    ;; 行间距优化
    (defun my/org-modern-spacing ()
      (setq-local line-spacing 0.15))
    (add-hook 'org-modern-mode-hook #'my/org-modern-spacing)
    (setq
     ;; 标题星号 - 渐进式圆形
     org-modern-star '("◉" "○" "◈" "◇" "⬡" "⬢")
     ;; 列表符号 - 统一箭头系
     org-modern-list '((43 . "▶")   ;; + 实心三角
                       (45 . "▸")   ;; - 空心三角
                       (42 . "•"))  ;; * 圆点
     ;; 复选框 - 统一方形
     org-modern-checkbox
     '((88 . "☑")   ;; [X] 完成
       (45 . "◫")   ;; [-] 进行中
       (32 . "☐"))  ;; [ ] 未完成
     ;; LaTeX 片段 - 数学符号
     org-modern-latex
     '((t . t)
       ("\\(" "⁅" "⁆")
       ("\\[" "⟦" "⟧")))))
(use-package org-src
  :ensure nil
  :after org
  :custom
  (org-src-window-setup 'current-window)
  (org-src-fontify-natively t)
  (org-src-tab-acts-natively t)
  (org-confirm-babel-evaluate nil)
  (org-src-preserve-indentation t)
  (org-edit-src-content-indentation 0)
  :bind (:map org-mode-map
         ("C-c C-v C-e" . org-babel-execute-src-block)
         ("C-c C-v C-b" . org-babel-execute-buffer)
         ("C-c '" . org-edit-special)  ;; 进入 org-src-mode
         :map org-src-mode-map
         ("C-c '" . org-edit-src-exit)  ;; 退出 org-src-mode
         ("C-c C-k" . org-edit-src-abort))
  :custom-face
  ;; 代码块使用等宽字体
  (org-block ((t (:inherit fixed-pitch :background "#2e3440"))))
  (org-block-begin-line ((t (:inherit fixed-pitch :foreground "#616e88"
                             :background "#2e3440" :extend t))))
  (org-block-end-line ((t (:inherit org-block-begin-line)))))
(use-package org-faces
  :ensure nil
  :after org
  :custom-face
  (org-level-1 ((t (:height 1.3 :weight bold))))
  (org-level-2 ((t (:height 1.2 :weight semi-bold))))
  (org-level-3 ((t (:height 1.1 :weight normal))))
  (org-document-title ((t (:height 1.5 :weight bold :underline t)))))
#+end_src
*** Org Agenda 提醒系统
#+begin_src emacs-lisp
(use-package appt
  :defer org-agenda
  :ensure nil
  :config
  (appt-activate 1)
  (setq appt-message-warning-time 15
        appt-display-interval 5
        appt-display-mode-line t
        appt-display-duration 60
        appt-audible t
        appt-display-diary nil
        appt-display-format 'window)

  (defun my/appt-display (min-to-app new-time msg)
    "自定义的提醒显示函数。"
    (let ((title (format "📅 Agenda 提醒 (%s分钟后)" min-to-app)))
      (appt-disp-window min-to-app new-time msg)
      (message "%s: %s" title msg)
      (when (featurep 'alert)
        (alert msg
               :title title
               :severity (cond
                          ((< (string-to-number min-to-app) 5) 'urgent)
                          ((< (string-to-number min-to-app) 10) 'high)
                          (t 'moderate))
               :category 'org-agenda))))

  (setq appt-disp-window-function #'my/appt-display)

  (defun my/org-agenda-to-appt ()
    "从 Org Agenda 文件中提取所有约会。"
    (interactive)
    (setq appt-time-msg-list nil)
    (org-agenda-to-appt t)
    (message "已从 Agenda 加载 %d 个约会提醒" (length appt-time-msg-list)))

  (run-at-time nil 3600 'my/org-agenda-to-appt)
  (add-hook 'org-finalize-agenda-hook 'my/org-agenda-to-appt)
  (add-hook 'org-after-todo-state-change-hook 'my/org-agenda-to-appt)
  (add-hook 'org-after-tags-change-hook 'my/org-agenda-to-appt)
  (my/org-agenda-to-appt)
  (message "Org Agenda 提醒系统已启用"))

(use-package alert
  :ensure t
  :after appt
  :config
  (setq alert-default-style  'message))

(defun my/show-today-appointments ()
  "显示今天所有的提醒事项。"
  (interactive)
  (if appt-time-msg-list
      (let ((buf (get-buffer-create "*Today's Appointments*")))
        (with-current-buffer buf
          (erase-buffer)
          (insert (propertize "📅 今日提醒事项\n\n"
                              'face '(:height 1.3 :weight bold)))
          (dolist (appt appt-time-msg-list)
            (insert (format "⏰ %s - %s\n"
                            (car appt)
                            (cadr appt))))
          (goto-char (point-min)))
        (display-buffer buf))
    (message "今天没有提醒事项")))

(defun my/clear-all-appointments ()
  "清除所有提醒事项。"
  (interactive)
  (setq appt-time-msg-list nil)
  (message "已清除所有提醒"))
#+end_src
*** Org导出
#+BEGIN_SRC emacs-lisp
;; 导出 HTML 时自动追加 org.css
(with-eval-after-load 'ox-html
  (setq org-html-head-extra
        "<link rel=\"stylesheet\" type=\"text/css\" href=\"https://gongzhitaao.org/orgcss/org.css\"/>"))
(with-eval-after-load 'ox-latex
  (setq org-latex-pdf-process '("latexmk -xelatex -quiet -shell-escape -f %f"))
  (push '(
          "\\documentclass[lang=cn]{article}
                 [NO-DEFAULT-PACKAGES]
                 [PACKAGES]
                 [EXTRA]"
          ("\\section{%s}" . "\\section*{%s}")
          ("\\subsection{%s}" . "\\subsection*{%s}")
          ("\\subsubsection{%s}" . "\\subsubsection*{%s}")
          ("\\paragraph{%s}" . "\\paragraph*{%s}")
          ("\\subparagraph{%s}" . "\\subparagraph*{%s}"))
        org-latex-classes))
#+end_src 
*** Org引用配置
#+begin_src emacs-lisp
(use-package oc
  :ensure nil
  :after org
  :custom
  (org-cite-global-bibliography my/bibliography-files)
  (org-cite-insert-processor 'basic)
  (org-cite-activate-processor 'basic)
  (org-cite-follow-processor 'basic)
  (org-cite-export-processors
   '((latex biblatex)
     (html csl)
     (t basic)))
  :config
  (setq org-cite-csl-styles-dir 
        (expand-file-name "csl-styles/" my/bibliography-directory))
  (unless (file-directory-p org-cite-csl-styles-dir)
    (make-directory org-cite-csl-styles-dir t)
    (message "请从 https://www.zotero.org/styles 下载 CSL 样式文件到: %s" 
             org-cite-csl-styles-dir)))
#+end_src
*** Org-babel配置文件
#+begin_src emacs-lisp
(with-eval-after-load 'org
  (org-babel-do-load-languages
   'org-babel-load-languages
   '((python . t)
     (R . t)
     (C . t))))
#+end_src
*** Markdown
#+BEGIN_SRC emacs-lisp
(use-package markdown-mode
  :ensure t
  :defer t
  :mode (("README\\.md\\'" . gfm-mode)
         ("\\.md\\'" . markdown-mode)
         ("\\.markdown\\'" . markdown-mode))
  :hook ((markdown-mode . visual-line-mode)
         (markdown-mode . display-fill-column-indicator-mode))
  :custom
  (markdown-command "pandoc")
  (markdown-fontify-code-blocks-natively t)
  (markdown-enable-math t)
  (markdown-header-scaling t)
  (markdown-header-scaling-values '(1.3 1.2 1.1 1.0 1.0 1.0))
  :custom-face
  (markdown-header-face-1 ((t (:height 1.3 :weight bold))))
  (markdown-header-face-2 ((t (:height 1.2 :weight bold))))
  (markdown-code-face ((t (:background "#2e3440")))))
(use-package markdown-toc
  :ensure t
  :defer t
  :after markdown-mode
  :bind (:map markdown-mode-map
         ("C-c C-t" . markdown-toc-generate-or-refresh-toc))
  :custom
  (markdown-toc-header-toc-title "## 目录"))
#+end_src

** 编程语言
*** Python的配置
#+begin_src emacs-lisp
(use-package uv-python
  :mode ("\\.py\\'" . python-ts-mode)
  :defer t
  :init
  ;; 基础 Python 设置
  (setq python-indent-offset 4
        python-shell-completion-native-enable nil)
  ;; 禁用内置 flymake
  (add-hook 'python-ts-mode-hook
            (lambda ()
              (remove-hook 'flymake-diagnostic-functions 'python-flymake t)))
  :hook
  ((python-mode python-ts-mode) . uv-python-mode)
  ((python-mode python-ts-mode) . display-line-numbers-mode)
  :custom
  (uv-python-venv-dir ".venv")
  (uv-python-auto-activate t)
  :bind (:map python-ts-mode-map
         ("C-c v a" . uv-python-activate)
         ("C-c v d" . uv-python-deactivate)
         ("C-c v s" . uv-python-switch)
         ("C-c v c" . uv-python-clear-cache))
  :config
  ;; 为py配置服务器
  (with-eval-after-load 'eglot
    (add-to-list 'eglot-server-programs
                 '((python-ts-mode python-mode)
                   . ("ty" "server"))))
  ;; 启用虚拟环境检测
  (uv-python-setup))
(use-package flymake-ruff
  :ensure t
  :hook (python-base-mode . flymake-ruff-load))
#+end_src
*** R配置文件
#+BEGIN_SRC emacs-lisp
;;; ESS - R 语言支持
(use-package ess
  :ensure t
  :defer t
  :mode (("\\.R\\'" . R-mode)
         ("\\.r\\'" . R-mode))
  :init
  ;; 性能优化配置
  (setq ess-use-flymake nil
        ess-use-eldoc 'script-only
        ess-eldoc-show-on-symbol nil
        ess-startup-directory 'default-directory
        ess-ask-for-ess-directory nil
        ess-indent-offset 2
        ess-style 'RStudio
        ess-offset-continued 2
        ess-expression-offset 2
        ess-nuke-trailing-whitespace-p t
        ess-default-style 'RStudio
        inferior-R-args "--no-save --no-restore")
  :hook
  (inferior-ess-r-mode . (lambda ()
                           (setq-local comint-scroll-to-bottom-on-input t
                                       comint-scroll-to-bottom-on-output t
                                       comint-move-point-for-output t)))
  :config
  ;; 简化字体锁定
  (setq ess-R-font-lock-keywords
        '((ess-R-fl-keyword:keywords . t)
          (ess-R-fl-keyword:constants . t)
          (ess-fl-keyword:fun-calls . t)
          (ess-fl-keyword:numbers . t)))
  ;; Eglot 集成
  (with-eval-after-load 'eglot
    (add-to-list 'eglot-server-programs
                 '((ess-r-mode) . ("air" "language-server"))))
  ;; 智能进程清理
  (defvar my/ess-cleanup-timer nil)
  (defun my/ess-cleanup-old-processes ()
    (dolist (proc (process-list))
      (when (and (string-match-p "^R:" (process-name proc))
                 (let ((last-activity (process-get proc 'last-activity)))
                   (and last-activity
                        (> (float-time (time-since last-activity)) 3600))))
        (delete-process proc))))
  (add-hook 'inferior-ess-mode-hook
            (lambda ()
              (unless my/ess-cleanup-timer
                (setq my/ess-cleanup-timer
                      (run-with-idle-timer 1800 t #'my/ess-cleanup-old-processes))))))
#+end_src
*** Elisp配置
#+BEGIN_SRC emacs-lisp
(use-package elisp-mode
  :ensure nil
  :bind (:map emacs-lisp-mode-map
         ("C-c C-x" . ielm)
         ("C-c C-c" . eval-defun)
         ("C-c C-b" . eval-buffer))
  :config
  (defun my/lisp-indent-function (indent-point state)
    "改进的 Lisp 缩进函数"
    (let ((normal-indent (current-column))
          (orig-point (point)))
      (goto-char (1+ (elt state 1)))
      (parse-partial-sexp (point) calculate-lisp-indent-last-sexp 0 t)
      (cond
       ((and (elt state 2)
             (or (not (looking-at "\\sw\\|\\s_"))
                 (looking-at ":")))
        (unless (> (save-excursion (forward-line 1) (point))
                   calculate-lisp-indent-last-sexp)
          (goto-char calculate-lisp-indent-last-sexp)
          (beginning-of-line)
          (parse-partial-sexp (point) calculate-lisp-indent-last-sexp 0 t))
        (backward-prefix-chars)
        (current-column))
       ((and (save-excursion
               (goto-char indent-point)
               (skip-syntax-forward " ")
               (not (looking-at ":")))
             (save-excursion
               (goto-char orig-point)
               (looking-at ":")))
        (save-excursion
          (goto-char (+ 2 (elt state 1)))
          (current-column)))
       (t
        (let* ((function (buffer-substring (point)
                                           (progn (forward-sexp 1) (point))))
               (method (or (function-get (intern-soft function)
                                         'lisp-indent-function)
                           (get (intern-soft function) 'lisp-indent-hook))))
          (cond ((or (eq method 'defun)
                     (and (null method)
                          (length> function 3)
                          (string-match "\\`def" function)))
                 (lisp-indent-defform state indent-point))
                ((integerp method)
                 (lisp-indent-specform method state indent-point normal-indent))
                (method
                 (funcall method indent-point state))))))))

  (add-hook 'emacs-lisp-mode-hook
            (lambda () (setq-local lisp-indent-function #'my/lisp-indent-function)))

  (add-hook 'help-mode-hook #'cursor-sensor-mode)

  (defun function-advices (function)
    "返回 FUNCTION 的所有 advice"
    (let ((flist (indirect-function function)) advices)
      (while (advice--p flist)
        (push (advice--car flist) advices)
        (setq flist (advice--cdr flist)))
      (nreverse advices)))

  (defun add-remove-advice-button (advice function)
    "为 ADVICE 添加移除按钮"
    (when (and (functionp advice) (functionp function))
      (let ((inhibit-read-only t)
            (msg (format "Remove advice `%s'" advice)))
        (insert "\t")
        (insert-button
         "Remove"
         'face 'custom-button
         'cursor-sensor-functions `((lambda (&rest _) (message ,msg)))
         'help-echo msg
         'action (lambda (_)
                   (when (yes-or-no-p msg)
                     (message "%s from function `%s'" msg function)
                     (advice-remove function advice)
                     (if (eq major-mode 'helpful-mode)
                         (helpful-update)
                       (revert-buffer nil t))))
         'follow-link t))))

  (defun add-button-to-remove-advice (buffer-or-name function)
    "在帮助缓冲区中添加移除 advice 的按钮"
    (with-current-buffer buffer-or-name
      (save-excursion
        (goto-char (point-min))
        (let ((ad-list (function-advices function)))
          (while (re-search-forward
                  "^\\(?:This function has \\)?:[-a-z]+ advice: \\(.+\\)$" nil t)
            (when-let* ((advice (pop ad-list)))
              (add-remove-advice-button advice function)))))))

  (define-advice describe-function-1 (:after (function) advice-remove-button)
    (add-button-to-remove-advice (help-buffer) function))

  (with-eval-after-load 'helpful
    (define-advice helpful-update (:after () advice-remove-button)
      (when helpful--callable-p
        (add-button-to-remove-advice (current-buffer) helpful--sym))))

  (defun remove-hook-at-point ()
    "在 *Help* 缓冲区中移除光标处的 hook"
    (interactive)
    (unless (memq major-mode '(help-mode helpful-mode))
      (error "仅适用于 help-mode 或 helpful-mode"))
    (let ((orig-point (point)))
      (save-excursion
        (when-let* ((hook (progn (goto-char (point-min)) (symbol-at-point)))
                    (func (when (and (or (re-search-forward "^Value:?[\s|\n]" nil t)
                                         (goto-char orig-point))
                                     (sexp-at-point))
                            (end-of-sexp)
                            (backward-char 1)
                            (catch 'break
                              (while t
                                (condition-case nil
                                    (backward-sexp)
                                  (scan-error (throw 'break nil)))
                                (when-let* ((bounds (bounds-of-thing-at-point 'sexp)))
                                  (when (<= (car bounds) orig-point (cdr bounds))
                                    (throw 'break (sexp-at-point)))))))))
          (when (yes-or-no-p (format "Remove %s from %s? " func hook))
            (remove-hook hook func)
            (if (eq major-mode 'helpful-mode)
                (helpful-update)
              (revert-buffer nil t))))))))
#+end_src
*** Treesit配置
#+BEGIN_SRC emacs-lisp
(use-package treesit
  :ensure nil
  :defer t
  :custom
  (treesit-extra-load-path (list (expand-file-name "etc/tree-sitter" user-emacs-directory)))
  ;; 语法源列表
  (treesit-language-source-alist
   '((python "https://github.com/tree-sitter/tree-sitter-python")
     (elisp "https://github.com/Wilfred/tree-sitter-elisp")
     (markdown "https://github.com/ikatyang/tree-sitter-markdown"))))
#+end_src
*** Format-all
#+begin_src emacs-lisp
(use-package format-all
  :ensure t
  :defer t
  :diminish
  :hook ((LaTeX-mode python-ts-mode c-mode ess-r-mode) . format-all-mode)
  :init
  (setq format-all-show-errors 'never)
  :config
  (setq format-all-default-formatters
        '(("LaTeX" auctex)
          ("Emacs Lisp" emacs-lisp)
          ("C" astyle)
          ("R" styler)
          ("Python" ruff)
          ("BibTeX" emacs-bibtex))))
(add-hook 'format-all-mode-hook #'format-all-ensure-formatter)
#+end_src
*** eglot-mode
#+begin_src emacs-lisp
(use-package eglot
  :hook ((prog-mode . (lambda ()
                        (unless (derived-mode-p
                                 'emacs-lisp-mode 'lisp-mode
                                 'makefile-mode 'snippet-mode
                                 'ron-mode)
                          (eglot-ensure)))))
  :init (setq eglot-autoshutdown t
              eglot-send-changes-idle-time 0.5
              eglot-extend-to-xref t
              eglot-code-action-indications '(eldoc-hint)
              eglot-events-buffer-config '(:size 0 :format full) ;; 取消 eglot log
              eglot-ignored-server-capabilities '(:documentFormattingProvider
                                                  :documentRangeFormattingProvider))
  :bind (:map eglot-mode-map
         ("C-c l r" . eglot-rename)
         ("C-c l a" . eglot-code-actions)))
;;; Flymake - 与 EGLOT 配合使用
(use-package flymake
  :diminish
  :bind ("C-c f" . flymake-show-buffer-diagnostics)
  :hook prog-mode
  :custom
  (flymake-no-changes-timeout nil)
  (flymake-fringe-indicator-position 'right-fringe)
  (flymake-margin-indicator-position 'right-margin))
#+end_src
*** 编程模式通用设置
#+begin_src emacs-lisp
(use-package eldoc
  :after eglot
  :bind
  (:map eglot-mode-map
   ("C-c C-d" . eldoc-doc-buffer))
  :init
  ;; 在屏幕右侧显示 eldoc-buffer
  (add-to-list 'display-buffer-alist
               '("^\\*eldoc.*\\*"
                 (display-buffer-reuse-window display-buffer-in-side-window)
                 (dedicated . t)
                 (side . right)
                 (inhibit-same-window . t)
                 (window-width . 80)))
  :config
  (setq eldoc-idle-delay 0.1)
  ;; 打开 eldoc-buffer 时关闭 echo-area 显示
  (setq eldoc-echo-area-prefer-doc-buffer t)
  ;; 将 minibuffer 窗口高度设为 1
  (setq max-mini-window-height 1)
  ;; 只单行显示 eldoc 信息
  (setq eldoc-echo-area-use-multiline-p nil))
(use-package rainbow-delimiters
  :ensure t
  :hook (prog-mode . rainbow-delimiters-mode)
  :config
  (setq rainbow-delimiters-max-face-count 5))
(use-package prog-mode
  :hook (prog-mode . display-fill-column-indicator-mode))
(use-package indent-bars
  :ensure t
  :hook (python-ts-mode . indent-bars-mode))
(use-package helpful
  :ensure t
  :bind (([remap describe-function] . helpful-callable)
         ([remap describe-command] . helpful-command)
         ([remap describe-variable] . helpful-variable)
         ([remap describe-key] . helpful-key)
         ([remap describe-symbol] . helpful-symbol)
         :map emacs-lisp-mode-map ("C-c C-d" . helpful-at-point)
         :map lisp-interaction-mode-map ("C-c C-d" . helpful-at-point)
         :map helpful-mode-map ("r" . remove-hook-at-point))
  :hook (helpful-mode . cursor-sensor-mode)
  :init
  (with-eval-after-load 'apropos
    (dolist (fun-bt '(apropos-function apropos-macro apropos-command))
      (button-type-put fun-bt 'action
                       (lambda (button)
                         (helpful-callable (button-get button 'apropos-symbol)))))
    (dolist (var-bt '(apropos-variable apropos-user-option))
      (button-type-put var-bt 'action
                       (lambda (button)
                         (helpful-variable (button-get button 'apropos-symbol))))))
  :config
  (defun my/helpful--navigate (button)
    "导航到 BUTTON 代表的路径"
    (find-file-other-window (substring-no-properties (button-get button 'path)))
    (when-let* ((pos (get-text-property button 'position (marker-buffer button))))
      (helpful--goto-char-widen pos)))
  (advice-add #'helpful--navigate :override #'my/helpful--navigate))
(use-package magit
  :ensure t
  :hook (git-commit-setup . git-commit-turn-on-flyspell)
  :bind (("C-x g" . magit-status)
         ("C-x M-g" . magit-dispatch)
         ("C-c M-g" . magit-file-dispatch))
  :custom
  (magit-diff-refine-hunk t)
  (magit-diff-paint-whitespace nil)
  (magit-ediff-dwim-show-on-hunks t))
(use-package gptel-magit
  :ensure t
  :hook (magit-mode . gptel-magit-install))
#+end_src
** LaTeX 配置
*** LaTeX Pretty Symbols
#+begin_src emacs-lisp
(add-hook 'prettify-symbols-mode-hook
          (defun prettify-symbols-latex-symbols ()
            "LaTeX 符号美化列表"
            (interactive)
            (setq-local prettify-symbols-alist '(("$" . 183)
                                                 ("\\alpha" . 945)
                                                 ("\\beta" . 946)
                                                 ("\\gamma" . 947)
                                                 ("\\delta" . 948)
                                                 ("\\epsilon" . 1013)
                                                 ("\\zeta" . 950)
                                                 ("\\eta" . 951)
                                                 ("\\theta" . 952)
                                                 ("\\iota" . 953)
                                                 ("\\kappa" . 954)
                                                 ("\\lambda" . 955)
                                                 ("\\mu" . 956)
                                                 ("\\nu" . 957)
                                                 ("\\xi" . 958)
                                                 ("\\pi" . 960)
                                                 ("\\rho" . 961)
                                                 ("\\sigma" . 963)
                                                 ("\\tau" . 964)
                                                 ("\\phi" . 981)
                                                 ("\\omega" . 969)
                                                 ("\\infty" . 8734)
                                                 ("\\int" . 8747)
                                                 ("\\sum" . 8721)
                                                 ("\\prod" . 8719)
                                                 ("\\leq" . 8804)
                                                 ("\\geq" . 8805)
                                                 ("\\in" . 8712)
                                                 ("\\subset" . 8834)
                                                 ("\\subseteq" . 8838)
                                                 ("\\supset" . 8835)
                                                 ("\\supseteq" . 8839)
                                                 ("\\cup" . 8746)
                                                 ("\\cap" . 8745)
                                                 ("\\emptyset" . 8709)
                                                 ("\\exists" . 8707)
                                                 ("\\forall" . 8704)
                                                 ("\\to" . 8594)
                                                 ("\\rightarrow" . 8594)
                                                 ("\\leftarrow" . 8592)
                                                 ("\\Rightarrow" . 8658)
                                                 ("\\Leftarrow" . 8656)
                                                 ("\\equiv" . 8801)
                                                 ("\\ne" . 8800)
                                                 ("\\approx" . 8776)
                                                 ("\\times" . 215)
                                                 ("\\cdot" . 8901)
                                                 ("\\partial" . 8706)
                                                 ("\\nabla" . 8711)))))
#+end_src
*** Lazytab
#+begin_src emacs-lisp
(use-package lazytab
  :vc (:url "https://github.com/karthink/lazytab" :rev :newest)
  :bind (:map orgtbl-mode-map
         ("<tab>" . lazytab-org-table-next-field-maybe)
         ("TAB" . lazytab-org-table-next-field-maybe))
  :after cdlatex
  :config
  (add-hook 'cdlatex-tab-hook #'lazytab-cdlatex-or-orgtbl-next-field 90)
  (dolist (cmd '(("smat" "插入 smallmatrix 环境"
                  "\\left( \\begin{smallmatrix} ? \\end{smallmatrix} \\right)"
                  lazytab-position-cursor-and-edit nil nil t)
                 ("bmat" "插入 bmatrix 环境"
                  "\\begin{bmatrix} ? \\end{bmatrix}"
                  lazytab-position-cursor-and-edit nil nil t)
                 ("pmat" "插入 pmatrix 环境"
                  "\\begin{pmatrix} ? \\end{pmatrix}"
                  lazytab-position-cursor-and-edit nil nil t)
                 ("tbl" "插入表格"
                  "\\begin{table}\n\\centering ? \\caption{}\n\\end{table}\n"
                  lazytab-position-cursor-and-edit nil t nil)))
    (push cmd cdlatex-command-alist))
  (cdlatex-reset-mode))
#+end_src

*** AUCTeX
#+begin_src emacs-lisp
(use-package latex
  :ensure auctex
  :mode ("\\.tex\\'" . LaTeX-mode)
  :hook ((LaTeX-mode . prettify-symbols-mode)
         (LaTeX-mode . display-line-numbers-mode)
         (LaTeX-mode . my/latex-with-outline))
  :bind (:map LaTeX-mode-map
         ("C-S-e" . latex-math-from-calc)
         ([remap LaTeX-environment] . cdlatex-environment)
         ("C-c C-v" . TeX-view)           ; 正向搜索：LaTeX → PDF
         ("C-c C-c" . TeX-command-master) ; 编译
         ("C-c C-a" . TeX-command-run-all)) ; 编译所有
  :config
  (setq TeX-auto-save t
        TeX-parse-self t
        TeX-save-query nil
        TeX-show-help nil
        TeX-PDF-mode t
        TeX-source-correlate-mode t
        TeX-source-correlate-start-server t
        TeX-error-overview-open-after-TeX-run t
        TeX-debug-warnings nil
        TeX-source-correlate-method 'synctex
        TeX-engine 'xetex
        TeX-command-default "Latexmk (xelatex)")

  (with-eval-after-load 'tex
    (dolist (cmd '(("Latexmk (xelatex)" "latexmk -xelatex -interaction=nonstopmode -synctex=1 %s" TeX-run-TeX nil t :help "使用 latexmk (XeLaTeX) 编译")
                   ("Latexmk Clean" "latexmk -c %s" TeX-run-command nil t :help "使用 latexmk 清理辅助文件")))
      (push cmd TeX-command-list))
    (setq TeX-view-program-selection '((output-pdf "PDF Tools"))
          TeX-view-program-list '(("PDF Tools" TeX-pdf-tools-sync-view)))
    ;; ----- 编译后自动刷新 PDF -----
    (add-hook 'TeX-after-compilation-finished-functions
              #'TeX-revert-document-buffer)
    (defun my/latex-with-outline ()
      (add-to-list 'minor-mode-overriding-map-alist
                   `(outline-minor-mode . ,outline-minor-mode-map))
      (outline-minor-mode 1))
    (defun latex-math-from-calc ()
      "对光标所在行的内容执行 calc 求值"
      (interactive)
      (let ((calc-settings '(calc-language latex
                                           calc-prefer-frac t
                                           calc-angle-mode rad)))
        (if (region-active-p)
            (let* ((beg (region-beginning))
                   (end (region-end))
                   (string (buffer-substring-no-properties beg end)))
              (delete-region beg end)
              (insert (calc-eval (cons string calc-settings))))
          (let ((l (thing-at-point 'line)))
            (end-of-line 1)
            (kill-line 0)
            (insert (calc-eval (cons l calc-settings)))))))
    (add-hook 'LaTeX-mode-hook
              (lambda ()
                (setq TeX-auto-untabify t
                      TeX-engine 'xetex
                      TeX-show-compilation t
                      TeX-save-query nil)
                (TeX-global-PDF-mode t)
                (imenu-add-menubar-index)))))

  (use-package preview
    :after latex
    :hook (LaTeX-mode . preview-larger-previews)
    :config
    (defun preview-larger-previews ()
      (setq preview-scale-function
            (lambda () (* 1.25 (funcall (preview-scale-from-face)))))))
#+end_src
*** CDLatex
#+begin_src emacs-lisp
(use-package cdlatex
  :ensure t
  :hook ((LaTeX-mode . turn-on-cdlatex)
         (cdlatex-tab . yas-expand)
         (cdlatex-tab . cdlatex-in-yas-field))
  :bind (:map cdlatex-mode-map
         ("<tab>" . cdlatex-tab))
  :config
  (with-eval-after-load 'yasnippet
    (define-key yas-keymap (kbd "<tab>") #'yas-next-field-or-cdlatex)
    (define-key yas-keymap (kbd "TAB") #'yas-next-field-or-cdlatex)
    (defun cdlatex-in-yas-field ()
      "在 Yas 字段中检查并处理 cdlatex"
      (when-let* ((_ (overlayp yas--active-field-overlay))
                  (end (overlay-end yas--active-field-overlay)))
        (if (>= (point) end)
            (let ((s (thing-at-point 'sexp)))
              (unless (and s (assoc (substring-no-properties s)
                                    cdlatex-command-alist-comb))
                (yas-next-field-or-maybe-expand)
                t))
          (let (cdlatex-tab-hook minp)
            (setq minp (min (save-excursion (cdlatex-tab) (point))
                            (overlay-end yas--active-field-overlay)))
            (goto-char minp)
            t))))
    (defun yas-next-field-or-cdlatex ()
      "正确处理 cdlatex 激活时跳转到下一个 Yas 字段"
      (interactive)
      (if (or (bound-and-true-p cdlatex-mode)
              (bound-and-true-p org-cdlatex-mode))
          (cdlatex-tab)
        (yas-next-field-or-maybe-expand)))))

(use-package org-table
  :after cdlatex
  :bind (:map orgtbl-mode-map
         ("<tab>" . lazytab-org-table-next-field-maybe)
         ("TAB" . lazytab-org-table-next-field-maybe))
  :hook (cdlatex-tab . lazytab-cdlatex-or-orgtbl-next-field))
#+end_src

*** Consult-RefTeX
#+begin_src emacs-lisp
(use-package consult-reftex
  :vc (consult-reftex :url "https://github.com/karthink/consult-reftex" :rev :newest)
  :after (reftex consult embark)
  :bind (:map reftex-mode-map
         ("C-c )" . consult-reftex-insert-reference)
         ("C-c M-." . consult-reftex-goto-label)
         :map org-mode-map
         ("C-c (" . consult-reftex-goto-label)
         ("C-c )" . consult-reftex-insert-reference))
  :config
  (with-eval-after-load 'embark
    (defun consult-reftex--key-finder ()
      (when (and
             (or (derived-mode-p 'LaTeX-mode)
                 (derived-mode-p 'org-mode))
             (cl-intersection
              '(font-lock-constant-face TeX-fold-unfolded-face)
              (ensure-list (get-char-property (point) 'face))))
        (save-excursion
          (text-property-search-backward
           'face 'font-lock-constant-face
           (lambda (val prop) (memq val (ensure-list prop))))
          (when (looking-back "\\(?:la\\)?\\(?:ref\\|bel\\){" (- (point) 5))
            (backward-char 1)
            (let* ((start (1+ (point)))
                   (end (progn (forward-list)
                               (1- (point)))))
              `(reftex-label
                ,(buffer-substring-no-properties start end)
                ,start . ,end))))))
    (cl-pushnew 'consult-reftex--key-finder embark-target-finders))
  (setq consult-reftex-preview-function
        #'consult-reftex-preview-make-window
        consult-reftex-preferred-style-order
        '("\\cref" "\\eqref" "\\ref"))
  (consult-customize consult-reftex-insert-reference
                     :preview-key (list :debounce 0.3 'any)))
#+end_src

*** Pdf-Tools
#+begin_src emacs-lisp
(use-package pdf-tools
  :ensure t
  :commands (pdf-view-mode pdf-tools-install)
  :mode ("\\.[pP][dD][fF]\\'" . pdf-view-mode)
  :config
  (pdf-tools-install) ; On demand loading, leads to faster startup time
  ;; 性能优化
  (setq pdf-view-midnight-colors '("#ffffff" . "#000000"))
  (setq-default pdf-view-display-size 'fit-page)
  (setq pdf-view-resize-factor 1.1)
  ;; 自动重载
  (add-hook 'pdf-view-mode-hook 'auto-revert-mode))
;; 常用辅助模块（它们都在 pdf-tools 包里，不需要 :ensure）
(use-package pdf-isearch
  :ensure nil :after pdf-tools)
(use-package pdf-links
  :ensure nil
  :after pdf-tools
  :bind (:map pdf-view-mode-map
         ("f" . pdf-links-isearch-link)
         ("F" . pdf-links-action-perform)))
(use-package pdf-annot
  :ensure nil
  :after pdf-tools)
(use-package doc-view
  :ensure nil
  :init
  (setq doc-view-mode-hook nil))
#+end_src
*** 文献管理与引用
**** Bibtex配置
#+begin_src emacs-lisp
(use-package bibtex
  :ensure nil
  :defer t
  :mode ("\\.bib\\'" . bibtex-mode)
  :custom
  (bibtex-dialect 'biblatex)  ;; 使用 BibLaTeX 格式
  (bibtex-user-optional-fields
   '(("keywords" "关键词，用逗号分隔")
     ("file" "PDF 文件路径")
     ("doi" "DOI")
     ("url" "在线链接")))
  (bibtex-align-at-equal-sign t)
  (bibtex-autokey-year-length 4)
  (bibtex-autokey-name-year-separator "")
  (bibtex-autokey-year-title-separator "")
  (bibtex-autokey-titleword-length 5)
  (bibtex-autokey-titlewords 3)
  :bind (:map bibtex-mode-map
         ("C-c C-c" . bibtex-clean-entry)))
#+end_src

**** RefTeX
#+begin_src emacs-lisp
(use-package reftex
  :defer t
  :hook ((LaTeX-mode . turn-on-reftex)
         (org-mode . turn-on-reftex))
  :config
  (setq reftex-plug-into-AUCTeX t
        reftex-cite-format 'biblatex))
#+end_src 
** 工具集成
*** Emacs 集成 GPG
#+begin_src emacs-lisp
(use-package epa-file
  :ensure nil  ;; Emacs 内置
  :config
  (epa-file-enable)
  (setq epa-file-cache-passphrase-for-symmetric-encryption t
        epa-file-select-keys t  ;; 自动选择密钥
        epg-pinentry-mode 'loopback))  ;; 在 minibuffer 输入密码
(use-package auth-source
  :ensure nil
  :config
  (setq auth-sources (list(locate-user-emacs-file "authinfo.gpg")))
  (setq auth-source-cache-expiry nil))
#+end_src
*** GPTel
#+begin_src emacs-lisp
(use-package gptel
  :ensure t
  :commands (gptel gptel-send gptel-menu gptel-abort)
  :config
  (setq gptel-default-mode 'org-mode
        gptel-use-curl nil
        gptel-model 'kimi-k2-thinking
        gptel-backend
        (gptel-make-openai "Aliyun Bailian"
          :host "dashscope.aliyuncs.com"
          :endpoint "/compatible-mode/v1/chat/completions"
          :stream t
          :key #'gptel-api-key-from-auth-source
          :models '(qwen-max qwen-plus qwen-turbo kimi-k2-thinking)))
  :bind (("C-c C-<return>" . gptel-menu)
         ("C-c <return>" . gptel-send)
         ("C-c C-g" . gptel-abort)
         :map gptel-mode-map
         ("C-c C-x t" . gptel-set-topic))
  :custom
  (gptel-prompt-prefix-alist
   '((markdown-mode . "### ")
     (org-mode . "* ")
     (text-mode . "### ")))
  (gptel-response-prefix-alist
   '((markdown-mode . "")
     (org-mode . "")
     (text-mode . ""))))
(defun replace-and-trim-latex (&rest _)
  "将 LaTeX 分隔符替换为 $, $$"
  (save-excursion
    (goto-char (point-min))
    (let ((replacements '(("\\\\(" . "$")
                          ("\\\\)" . "$")
                          ("\\\\\\[" . "$$")
                          ("\\\\\\]" . "$$"))))
      (dolist (pair replacements)
        (goto-char (point-min))
        (while (re-search-forward (car pair) nil t)
          (replace-match (cdr pair) nil nil))))
    (goto-char (point-min))
    (while (re-search-forward "\\(?:^\\| \\)\\$\\(?: \\|$\\)" nil t)
      (replace-match "$" nil nil))))
(add-hook 'gptel-post-response-functions #'replace-and-trim-latex)
#+end_src

*** Persistent Scratch
#+begin_src emacs-lisp
(use-package diminish
  :ensure t)
(use-package persistent-scratch
  :ensure t
  :diminish
  :bind (:map persistent-scratch-mode-map
         ([remap kill-buffer] . (lambda (&rest _)
                                  (interactive)
                                  (user-error "Scratch 缓冲区无法关闭")))
         ([remap revert-buffer] . persistent-scratch-restore)
         ([remap revert-this-buffer] . persistent-scratch-restore))
  :hook ((after-init . persistent-scratch-autosave-mode)
         (lisp-interaction-mode . persistent-scratch-mode))
  :init
  (setq persistent-scratch-save-file
        (locate-user-emacs-file "etc/persistent-scratch/.persistent-scratch")
        persistent-scratch-backup-file-name-format "%Y-%m-%d"
        persistent-scratch-backup-directory
        (locate-user-emacs-file "etc/persistent-scratch/")))
#+end_src

*** Transient 配置
#+begin_src emacs-lisp
(setq transient-levels-file (locate-user-emacs-file "etc/transient/levels.el")
      transient-values-file (locate-user-emacs-file "etc/transient/values.el")
      transient-history-file (locate-user-emacs-file "etc/transient/history.el"))
#+end_src
*** Vundo
#+begin_src emacs-lisp
(use-package vundo
  :ensure t
  :bind ("C-x u" . vundo)
  :config
  (setq vundo-glyph-alist vundo-unicode-symbols))
#+end_src
*** 日历配置
#+begin_src emacs-lisp
(use-package cal-china-x
  :ensure t
  :defer t
  :commands cal-china-x-setup
  :hook (calendar-mode . cal-china-x-setup)
  :config
  (setq calendar-mark-holidays-flag t
        cal-china-x-important-holidays cal-china-x-chinese-holidays
        cal-china-x-general-holidays '((holiday-lunar 1 15 "元宵节")
                                       (holiday-lunar 7 7 "七夕节")
                                       (holiday-fixed 3 8 "妇女节")
                                       (holiday-fixed 3 12 "植树节")
                                       (holiday-fixed 5 4 "青年节")
                                       (holiday-fixed 6 1 "儿童节")
                                       (holiday-fixed 9 10 "教师节"))
        holiday-other-holidays '((holiday-fixed 2 14 "情人节")
                                 (holiday-fixed 4 1 "愚人节")
                                 (holiday-fixed 12 25 "圣诞节")
                                 (holiday-float 5 0 2 "母亲节")
                                 (holiday-float 6 0 3 "父亲节")
                                 (holiday-float 11 4 4 "感恩节"))
        calendar-holidays (append cal-china-x-important-holidays
                                  cal-china-x-general-holidays
                                  holiday-other-holidays)))
;;; init.el ends here
#+end_src
